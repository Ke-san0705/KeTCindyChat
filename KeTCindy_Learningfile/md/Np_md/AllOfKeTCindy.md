
# 平面の図形とグラフ  
## 設定・定義  
### 環境設定  
#### `Ketinit(options)`  
KeTCindyを初期化する。これがないとKeTCindyの関数等を使えないため、**必ず1行目に記述し、普通はオプションは不要**  
【例】  
```  
Ketinit();//初期化  
  
Listplot("1",[A,B,C,A],["dr,2"]);  
  
Windispg();  
```  
  
#### `Initglist()`, `Setglist()`, `Addglist()`  
ketlib スロットで作られる描画データを描画リストに追加する。  
`Implicitplot`,`Hatchdata` など実行時間のかかるコマンドを figures スロットにおくと，その都度実行されてしまう。それを避けるため ketlib スロットにおいたときに用いる。  
【例】  
Initializationスロットに以下を追記する  
```  
Initglist(); // 描画データを初期化  
Implicitplot('1',fun,rng);//関数fun範囲rngの陰関数のグラフを描く  
Setglist(); //描画データをセット  
```  
以下はDrawスロットに書く。  
```  
Ketinit();   
Addglist();//Initializationスロットでセットした描画データをまとめて追加する  
```  
  
#### `Setfiles(filename)`  
出力するファイル名の設定  
出力する Tex のファイル名を指定する。  
出力するファイル名は 初期設定では，作図している Cinderella のファイル名。  
たとえば，triangle.cdy で作図して出力すると，triangle.tex ができる。  
これに対し，triangle.cdy で作図しているときに，grav.tex で出力したい場合は  
```  
Setfiles("grav");  
```  
とすると，grav.tex ができる。  
  
#### `Setparent(filename)`  
Parent ボタンで出力するファイル名の設定  
Figpdf() を使って Parent ボタンで出力する Tex のファイル名を指定する。  
Parent ボタンで出力するファイル名は 初期設定がないので，指定する必要がある。  
たとえば，triangle.cdy で作図しているときに，図サイズの grav.pdf を作る場合，  
```  
Setparent("grav");  
```  
とすると，図の TeX ファイル triangle.tex と PDF を作る grav.tex ができ，ここから grav.pdf ができる。  
  
#### `Changework(path)`  
作業ディレクトリを指定（変更）する  
作業ディレクトリは， 初期設定では，現在作図しているファイルのあるフォルダ（ディレクトリ）の fig フォルダである。これを変更する。  
  
#### `Addpackage(パッケージ名)`  
TeX のパッケージを追加する  
パッケージ名は1つの時文字列で、1つ以上の時リスト形式で記述する。  
なお、これはInitializationスロットに追記する形で記述するため注意すること。  
  
【例】emathパッケージを追加する。  
```  
//以下のどちらか  
Addpackage("emath");  
Addpackage(["emath"]);  
```  
  
#### `Usegraphics("tpic"/"pict2e"/"tikz")`  
TeXのグラフィクスパッケージを変更する。  
なお、これはInitializationスロットに追記する形で記述するため注意すること。  
【例】TeX出力時に使うグラフィックスパッケージをtikzに変更する。  
```  
Usegraphics("tikz")  
```  
引数は文字列であるが、許されているのは"tpic"/"pict2e"/"tikz"の3つである。  
  
### 描画設定・定義  
#### `Addax(0 or 1)`  
座標軸の表示を選択する。  
  
【例】座標軸を表示する。  
```  
Addax(1);  
```  
【例】座標軸を表示しない。  
```  
Addax(0);  
```  
Addaxを書かなければ、表示はそのままとなる。座標軸のあるなしを明示したいときだけこれを記述する。  
  
#### `Setax()`  
座標軸の書式を設定する。  
引数はリストで与え、順番に、  
1.軸の形状(直線は "l" ，矢印は "a") 初期設定は直線  
矢印の大きさの倍率を指定するときは，"a0.5"のようにする。  
また，矢印のスタイルは Setarrow で指定する。  
2.横軸名 初期設定は "x"  
3.横軸名の位置 初期設定は "e"  
4.縦軸名 初期設定は "y"  
5.縦軸名の位置 初期設定は "n"  
6.原点名 初期設定は "O"（文字として書かれる）  
7.原点名の位置 初期設定は "sw"  
8.線種  
9.線の色  
10.ラベルの色   
それぞれダブルクォートでくくる。色は，色名が使える。"red" など。  
  
  
【例】座標軸の先端を矢印にし，原点の北西に O を書く。  
```  
Setax(["a","","","","","","nw"]);//第8引数以降は省略  
```  
【例】原点の北西に O を書く。  
```  
Setax([7,"nw"]);//第7引数のみ指定  
```  
【例】軸を赤の点線にする。  
```  
Setax(["","","","","","","","do","red"]);  
```  
【例】先端を矢印にし，横軸を θ，縦軸を x にして矢じりの左側に書く。  
```  
Setax(["a","\theta","","x","w"]);  
```  
  
#### `Drwxy(), Drwxy(options)`  
指定する手順で座標軸を描く  
座標軸は 初期設定では最後に描かれるが，座標軸上に白抜きの点を表示するなど，先に描くことが必要な場合に用いる。描画面には座標軸は表示されない。  
options は次のオプションが許されている。  
`["Origin=", "Xrng=", "Yrng="]`  
  
#### `Definecolor(色名 , 定義のリスト)`  
色名を定義する  
ユーザー命名の色名を定義する。定義リストは RGB または CMYK のリスト  
各色０〜１の範囲で指定する。定義した色名は，`Setcolor(color,options)` で使うことができる。  
なお，KETCindy では，68 色を色名で使うことができる。色の名称はカラーコード一覧参照。  
【例】暗い紫色を darkmaz の名称で定義して使う。  
```  
Definecolor("darkmaz",[0.8,0,0.8]);//(R,G,B)=(0.8,0,0.8)の色をdarkmazとして定義  
Setcolor("darkmaz");//darkmaz色をsetcolorする  
```  
  
#### `Setcolor(color,options)`  
描画色の設定  
引数 color はカラーコードまたは色の名称。  
カラーコードは RGB または CMYK をリストで与える。各色０～１。  
色の名称はカラーコード一覧 の 68 色が指定できる。  
```  
Ketinit();  
  
C.xy=|B.xy|/|C.xy|*C.xy;  
Listplot([B,A,C]);  
Setcolor("red");//描画色のデフォルトを赤色にする。ここより下ではSetcolorが再度実行されるまで赤色で描画される。  
Anglemark("1",[B,A,C],[3]);   
Arrowhead(C,"ag1",1);  
Setcolor("black");  
  
Windispg();  
```  
  
**座標軸を描く場合は，このあと `Setcolor("black")` で黒に戻しておかないと，座標軸が赤で表示されてしまうので要注意。**  
  
#### `Deffun(関数名 , 定義のリスト)`  
関数を定義する  
関数定義は，CindyScript の関数定義 f(x):=式 でもできるが，Deffun() を使うことにより，R でこの関数を利用することができる。目的に応じて使い分けるとよい。  
式のリストには if 文を用いた場合分けの関数式を記述することもできる。  
【例】$$f\left(x\right)=\frac{1}{x^2+1}$$を定義し、グラフを描く。  
```  
Ketinit();  
  
Deffun("f(x)",["regional(y)","y=1/(x^2+1)","y"]);//f(x)=1/(x^2+1)として定義する  
Plotdata("1","f(x)","x");//定義したf(x)のグラフを描く。  
  
Windispg();  
```  
  
【例】  
$$   
f\left(x\right)= \begin{cases}  
   1 & x\geq 0 \\  
   -1 & x< 0  
\end{cases}   
$$  
を定義し、グラフを描く。  
```  
Ketinit();  
Deffun("f(x)",["regional(y)","if(x>=0,y=1,y=-1)","y"]);//if文によって、x>=0のときはy=1,そうでないときはy=-1としている  
Plotdata("1","f(x)","x",["Dis=1","Num=1000"]);//Disオプションで値が1以上ジャンプする点は線を描かない  
  
Windispg();  
```  
また以下のように、if 文はネストすることができる。  
```  
Deffun("f(x)",["regional y","if(x>1,y=1,if(x>-1,y=x,y=-1))","y"]);  
```  
  
## `Defvar(文字列)`  
変数を定義する  
#### 説明  
変数の定義を R と共有する。  
【例】constを3と定義する  
```  
Defvar("const=3");  
```  
  
【例】複数の変数を定義するときはリストにする。  
```  
Defvar(["a",3,"b",1]);  
```  
  
#### `Fontsize(記号)`  
フォントサイズを設定する  
次に `Fontsize()` を実行するまで有効  
記号は，`"t" , "ss" , "f", "s" , "n" , "la","La", "LA", "h" , "H"`  であり、この順番で大きくなる。  
【例】作図ツールの「点を加える」で，A～G の点をとっておく。小さい方からいくつか表示する。  
```  
Fontsize("t"); //一番小さくする。これは次に別の大きさで指定する時まで有効。  
```  
  
#### `Ptsize(n) , Setpt(n)`  
表示する点の大きさを設定する。  
`Ptsize()` と `Setpt()` は同じである。 初期設定は１  
全体の点の大きさを設定する。  
【例】  
```  
Ptsize(4);//点のサイズを4に変更。次実行するまで有効  
```  
  
点の大きさを個々に変えたい場合は，以下のようにsize オプションを用いる。  
【例】１から 4 までの点の大きさ  
```  
Pointdata("1",A,["Size=1"]);  
Pointdata("2",B,["Size=2"]);  
Pointdata("3",C,["Size=3"]);  
Pointdata("4",D,["Size=4"]);  
```  
  
#### `Setmarklen(数)`  
座標軸の目盛の長さを設定する  
`Htickmark()` , `Vtickmark()` で座標軸に目盛を入れるとき，その長さを設定する。  
  
#### `Setorigin(座標)`  
描画する座標軸の原点を設定（移動）する。座標系は変化しない。  
描画する座標軸の原点を引数の座標とする。座標は点の識別名でもよい。  
【例】原点を`(3,2)` として座標軸を描く。  
```  
Setorigin([3,2]);  
```  
【例】原点を点 A の位置にして座標軸を描く。  
```  
Setorigin(A);  
```  
  
#### `Setpen(数)`  
線の太さの初期値を設定する  
`Listplot()`などの描画関数のオプション dr で，個々の太さは指定できる。  
  
#### `Setscaling(倍率)`  
縦方向の倍率を設定する。倍率は実数またはリスト。実数の場合は縦方向，リストの場合は [横方向，縦方向]  
の指定となる。  
2 次関数の応用問題などでは，グラフが縦に大きくなる場合があり，y 軸方向のスケーリングを変えたいこと  
がよくある。次のスクリプトは，f(x) = −x^2 + 10x のグラフを縦軸方向を半分にして描くものである。  
  
```  
Ketinit();  
  
Setscaling(0.5);//全体の大きさを0.5にする  
A = createpoint("A",[0,25/4]);  
B = createpoint("B",[5/2,25/4]);  
C = createpoint("C",[5/2,0]);  
Listplot([A,B],["do"]);  
Listplot([C,B],["do"]);  
Plotdata("1","-2*x^2+10*x","x");  
Letter([[5,0],"s2w","5",[0,25/2],"w2",  
"$\frac{25}{2}$",C,"s4","$\frac{5}{2}$"]);  
  
Windispg();  
```  
なお、Cinderella で作図した幾何要素に対しては Setscaling は無効である。  
  
#### `Setunitlen(文字列)`  
単位長を設定する。 初期設定は 1cm。  
この関数は，スクリプトの初めの方に書くのがよい。  
【例】文字幅を8mmにする  
```  
Setunitlen("8mm")  
```  
  
#### `Setwindow(x の範囲, y の範囲)`  
出力する描画領域を設定する。  
出力する描画領域は，通常は 2 点 SW と NE を対角とする矩形領域である。  
この 2 点をドラッグすることによりビジュアルに描画領域を決められる。  
しかし，これとは別に出力範囲を設定したい場合にこの関数を用いる。  
また，表を作成したときは，表の範囲が出力範囲として優先される（`Tabledata()` を実行したとき）ので，表外に図を描いた場合は，最後にこの関数で出力範囲を指定して書き出す。  
【例】[-5,5]を[-1,3]頂点に持つ長方形を描画範囲として定める。  
```  
Setwindow([-5,5],[-1,3]);  
```  
  
## 描画  
### 描画  
描画関数は曲線などを作図する関数である。  
基本的な書式は次の通り。  
```  
関数名 (name , 点リストなど , options);  
```  
name は，プロットデータの名称で，関数ごとに決められた頭部のあとに付けられる。たとえば，線分を描く`Listplot()` でできるプロットデータは，頭部が"sg"であり，name を"1"とすれば，"sg1"という名称のプロットデータができる。name 指定は不要の場合もあり，その場合は KETCindy が自動的に名称を作成する。なお，name に演算記号は使えないので，番号として負の数は使えない。  
点リストなどには，点の座標，点の識別名，複数の点のリスト，複数の点を示す文字列などがあり，関数によって異なる。点は Cinderella で作図した幾何要素の点を利用できる。  
`options` は，線種・表示する文字列・解像度・出力の有無などを指定するオプション群。  
  
線種はつぎの 4 通り。 初期設定は実線。  
| オプション |                                                                                                          |  
|------------|----------------------------------------------------------------------------------------------------------|  
| dr,n       | 太さ n の実線で描く。                                                                                    |  
| da,m,n     | 破線を描く。 mは破線の長さ、nは破線の間隔(m,nは省略可) m,nオプションはcinderellaの描画面には反映されない |  
| id,m,n     | ギャップからはじまる破線を描く。                                                                         |  
| do,m,n     | 点線で描く。 mは点の間隔,nは太さ(m,nは省略可)                                                            |  
  
描画色指定は，RGB または CMYK のリストで指定するか，色名を用いる。  
【例】  
```  
"Color=[0,0.7,0]"  
```  
  
出力するかどうかなど  
| オプション |                                     |  
|------------|-------------------------------------|  
| notex      | Cinderella 画面上の図形を出力しない |  
| nodisp     | Cinderella 画面上にも出力しない     |  
  
"nodisp"は画面上にも，Ｒへのデータにも出力されないが，プロットデータは作成されるので，プロットデータだけを利用したい場合に有効である。  
【例】A中心、Bを通る円を描画するがCinderella上には出力しない。  
```  
pdata=Circledata([A,B],["nodisp"]);  
```  
として，後にプロットデータ pdata を利用する。  
  
その他  
| オプション |                                               |  
|------------|-----------------------------------------------|  
| Size=n     | 点の大きさ，線の太さの指定                    |  
| Num=n      | 曲線の場合の分割数(プロットデータの個数 +1)|  
  
特に断りがなければ以上のオプションが許されている。  
  
### 点・線分・直線  
#### `Pointdata(name , 点リスト , options)`  
点のデータを作成する。  
与えられた座標の点データを作成する。オプションは"Size=","Color=","Inside="が許されている。  
Inside オプションは，点の内部についての指定（n : 内部を塗らない，color:内部色）  
  
【例】座標指定で 2 つの点データを作る。  
```  
Pointdata("1",[[1,2],[-2,3]]);  
```  
【例】  
座標指定で 2 つの点データを作る。  
```  
Pointdata("1",[[1,2],[-2,3]]);  
```  
作図した点 A,B について，点データを作る。  
```  
Pointdata("1",[A,B]);  
```  
A,B が作図されていない場合は作成されない。  
Cinderella の描画面上では既存の点 A,B に黒の点が重なって表示される。  
A の位置に大きさ 4 で点を作る。  
```  
Pointdata("1",A,["Size=4"]);  
```  
点データを作り，オプション（内部を塗らない）で描く  
```  
Pointdata("1",[A,B],["Inside=n"]);  
```  
点データを作り，オプション（内部を白塗り）で描く  
```  
Pointdata("1",[A,B],["Inside=white"]);  
```  
点データを作るが，TeX には出力しない  
```  
Pointdata("1",[[3,4],[5,6]],["notex"]);  
```  
点データを作るが，TeX には出力せず画面上にも表示しない。  
```  
Pointdata("1",[[3,4],[5,6]],["nodisp"]);  
```  
節点を明示した木を描く  
```  
Ptsize(3);  
Pointdata("1",[[1,2],[3,4],[5,2]]);  
Listplot("1",[[0,0],[1,2],[3,4],[5,2],[4,0]]);  
Listplot("2",[[1,2],[2,0]]);  
Listplot("3",[[5,2],[6,0]]);  
```  
  
#### `Putpoint(点名 , 座標1 , 座標 2 )`  
点を動的に作る  
識別名が点名の点を，既存でなければ座標１に作る。既存ならば座標 2 に移動する。Tex には出力されない。  
  
【例】点 A を作る。  
(1,1) に固定点 A を作る。 この点は動かすことができない。  
```  
Putpoint("A",[1,1]);  
```  
(1,1) に自由点を作るには次のようにする。  
```  
Putpoint("A",[1,1],[A.x,A.y]);  
```  
この点は座標 2 の効果により，自由点となり，ドラッグして動かすことができる。  
  
他の関数で点データを扱う場合はこれか作図ツールを用いて点を作図する必要がある。  
  
#### `Putintersect(点名 , PD1 , PD2 , [No] )`  
2 曲線の交点を作る  
PD1,PD2 は 2 曲線のプロットデータ名。作成される点は幾何点。  
描画範囲に交点が１つだけのとき，第 4 引数がなくても交点が作られる。  
描画範囲に 2 つ以上の交点がある場合，第 4 引数を省略するとコンソールに交点の座標のリストと，「Choose point number 」というガイドが表示される。そこで，引数の No として，その番号を指定すると，その点が作られる。この関数で作成されるのは幾何点だけなので，TEX の図に点として明示するためには Pointdata() で書き出す。  
注）交点が存在しない場合は，「No intersect point」がコンソールに表示される。  
次の例は，3 次曲線と直線の交点を 3 つとも取ったものである。  
```  
Plotdata("1","x^3-4*x","x",["Num=200"]);//x^3-4*xのグラフを作成し、プロットデータgr1を作成する  
Plotdata("2","1/2*x+1","x");//1/2*x+1のグラフを作成し、プロットデータgr2を作成する  
Putintersect("P","gr1","gr2",1);//gr1とgr2の交点のうち  1番目を点Pとする。作図ツール等であらかじめ点を打つ必要はない  
Putintersect("Q","gr1","gr2",2);//gr1とgr2の交点のうち  2番目を点Qとする。作図ツール等であらかじめ点を打つ必要はない  
Putintersect("R","gr1","gr2",3);//gr1とgr2の交点のうち  3番目を点Rとする。作図ツール等であらかじめ点を打つ必要はない  
Pointdata("1",[P,Q,R],["size=4"]);  
```  
  
#### `Putoncurve(点の名前, プロットデータ, options)`  
曲線上に点を乗せる。  
点が存在しない場合は新たに作る。すでにその点が存在する場合は，その点の x 座標を使う。初期値の x 座標の 初期設定は 0。  
options は，x 座標の範囲をリストで与える。  
【例】アステロイド上の動点 P をとる。  
```  
Paramplot("1","[2*cos(t)^3,2*sin(t)^3]","t=[0,2*pi]");//アステロイド曲線(媒介変数表示で[2*cos(t)^3,2*sin(t)^3])を描画しプロットデータgp1を作成する  
Putoncurve("P","gp1",[-1,1]);//gp1上の[-1,1]にPを置く  
```  
点 P がアステロイド上にでき，この点はドラッグするとアステロイド上を −1 ≤ x ≤ 1 の範囲で動かすことができる。ただし，-1,1 の付近は y 座標の判断の関係でぴったりはいかない。  
  
#### `Putonline(点名 , 座標１ , 座標 2 )`  
直線上に点を作る  
座標１，座標 2 を通る直線上に点名の点を作る。できた点は直線上しか動かない  
  
【例】点 A, B を通る直線上に点 P をとる。  
```  
Putonline("P",A,B);  
```  
  
#### `Putonseg(点名 , 座標１ , 座標 2 )`  
線分上に点を作る  説明  
座標１，座標 2 を端点とする線分上に点名の点を作る。できた点は線分上しか動かない。指定した点がすでに存在する場合は動かさない。  
【例】  
線分 AB 上に点 C をとる。  
```  
Putonseg("C",A,B);  
```  
点 (-1,0),(2,2) を通る線分上に点 C をとる。  
```  
Putonseg("C",[[-1,0],[2,2]]);  
```  
  
#### `Reflectpoint(点，対称点または対称軸)`  
点の鏡映の座標を返す。  
点を指定された点または軸に関して対称移動した点の座標を返す。対称軸は [ 点 1, 点 2 ] で指定  
  
【例】点 A～F を作図しておき，C～F を A の鏡映の位置に配置する。  
Ｃは B に関して A と対称な点  
Ｄは点 (2,3) に関して A と対称な点  
E は点 (1,0) に関して (-1,1) と対称な点  
F は直線 CE に関して A と対称な点  
  
```  
C.xy=Reflectpoint(A,B);  
D.xy=Reflectpoint(A,[[2,3]]);  
E.xy=Reflectpoint([-1,1],[[1,0]]);  
F.xy=Reflectpoint(A,[C,E]);  
```  
  
#### `Rotatepoint(点 ,角度 , 中心)`  
点の位置を回転する  
点を，中心で示された点の周りに回転した座標を返す。角度は弧度法で与える  
点 A～E は作図しておき，C～E をそれぞれの位置に配置する。  
点 C は A を，Ｂに関して\frac{2}{3}\piだけ回転した点  
点 D は点 (5,2) を，Ｂに関して \frac{\pi}{3}だけ回転した点  
点 E は点 (3,0) を A に関して -\frac{\pi}{4}だけ回転した点  
```  
C.xy=Rotatepoint(A,2*pi/3,B);  
D.xy=Rotatepoint((5,2),pi/3,B);  
E.xy=Rotatepoint([3,0],-pi/4,A);  
```  
  
#### `Scalepoint(点，比率ベクトル，中心)`  
点の位置の拡大・縮小を行う  
点を，指定された中心を原点とする座標系で，比率ベクトルの分だけ拡大・縮小した位置の座標を返す。  
【例】点 A～F は作図ツールで適当な位置にとっておく。  
点 D を，点 A を原点を中心に横に 3 倍，縦に 2 倍した位置に置く。  
点 E を，点 A を点 B を中心に横に 3 倍，縦に 2 倍した位置に置く。  
点 F を，点 A を原点を中心にベクトル −−→OC で示された比率の位置に置く。  
```  
D.xy=Scalepoint(A,[3,2],[0,0]);  
E.xy=Scalepoint(A,[3,2],B);  
F.xy=Scalepoint(A,C.xy,[0,0]);  
Arrowdata("1",[[0,0],C]);  
Pointdata("1",[A,B,C,D,E,F],["size=2"]);  
Letter([A,"e2","A("+A.x+","+A.y+")"]);  
Letter([B,"e2","B("+B.x+","+B.y+")"]);  
Letter([C,"e2","C("+C.x+","+C.y+")"]);  
Letter([D,"e2","D("+D.x+","+D.y+")"]);  
Letter([E,"e2","E("+E.x+","+E.y+")"]);  
Letter([F,"e2","F("+F.x+","+F.y+")"]);  
```  
点 A,B,C をドラッグすると，インタラクティブに D,E,F の位置が変わる。  
  
#### `Translatepoint(点 , 移動ベクトル)`  
点を平行移動する  
点を移動ベクトルで示された分だけ平行移動した点の座標を返す  
【例】点 A～D は作図しておく。  
点 C を点 A を x 軸方向に 2 , y 軸方向に 3 だけ平行移動した点にする。  
点 D を点 A をベクトル OB だけ平行移動した点にする。  
```  
C.xy=Translatepoint(A,[2,3]);  
D.xy=Translatepoint(A,B.xy);  
```  
  
#### `Setarrow([arrowsize,angle,position,cut])`  
Arrowdata,Arrowhead で描く矢印のスタイルを設定する。  
arrowsize,angle,position,cut,linestyle は，順に大きさ (1)，開き角 (18)，位置 (1)，切り込み (0.2) である。  （カッコ内はデフォルト値）  
  
#### `Arrowdata(name,[始点 , 終点] , options)`  
2 点間を結ぶ矢線を描く。  
name はなくてもよい（自動的に通し番号をつける）。  
options は矢じりの形状などの指定（リストで与える）。  
数値は， 大きさ, 開き角, 位置, 切り込み  
`"Line=n(y)"`（矢印は線だけ），`"Cutend="`（トリミング），`"Color="`  
開き角は 60 分法で与える。2.5 未満の時は 18°の倍数指定とする。  
矢じり位置は，線分の長さを１とした始点からの距離。  
切り込みのデフォルトは 0.2  
トリミング :`"Cutend=m"` または `"Cutend=[m,n]"` で，右辺が数のときは両端を m だけカットする。リストのときは始点を m, 終点を n だけカットする。m が負のときは延長する。  
  
【例】オプションの設定とその結果を示す。  
```  
Arrowdata("1",[A,B]);//点Aから点Bに矢印を引く  
Arrowdata("2",[[1,0],[2,3]],[2]);//(1,0)から(2,3)に大きさ2の矢印を引く  
Arrowdata("3",[[2,0],[3,3]],[3,45]);//(2,0)から(3,3)に大きさ3,開き角45の矢印を引く  
Arrowdata("4",[[3,0],[4,3]],[3,1,0.5]);//(3,0)から(4,3)に大きさ3,開き角1,位置0.5の矢印を引く  
Arrowdata("5",[[4,0],[5,3]],[3,1,1,0]);//(4,0)から(5,3)に大きさ3,開き角1,位置1,切り込み0の矢印を引く  
Arrowdata("6",[[5,0],[6,3]],[3,"Line=y"]);//(5,0)から(6,3)に大きさ3,線だけの矢印を描画する  
Arrowdata("7",[[6,0],[7,3]],[3,"dr,2"]);//(6,0)から(7,3)に大きさ3,実践で線の太さ2の矢印を引く  
Arrowdata("8",[[7,0],[8,3]],[3,1,1,0.5,"Color=red"]);//(7,0)から(8,3)に大きさ3,開き角1,位置1,切り込み0.5,色が赤色の矢印を引く  
```  
  
#### `Arrowhead(点 , 方向 , options)`  
指定された位置に，指定された方向を向いた矢じりだけを描く。  
点は座標または幾何要素名。方向は原点から見て座標 [a,b] の方向。  
  
optionsはsetarrowと同じ。  
  
【例】  
```  
Arrowhead(A,[-1,1]);//Aの位置にAからみて(-1,1)の位置に(座標ではない)矢じりだけを描く  
Arrowhead([1,1],[-1,1],[2,60]);//(1,1)の位置に(1,1)からみて(-1,1)の位置に(座標ではない)大きさ2,開き角60の矢じりだけを描く。  
Arrowhead(A,[-1,1],[2,30,0.5]);//Aの位置にAからみて(-1,1)の位置に(座標ではない)大きさ2,開き角30,位置0.5の矢じりだけを描く。  
Arrowhead([1,1],[-1,1],[2,20,0.5,"Line=y"]);//(1,1)の位置に(1,1)からみて(-1,1)の位置に(座標ではない)大きさ2,開き角20,位置0.5で線だけの矢じりを描く。  
```  
  
#### `Arrowhead(点または位置比 , プロットデータ, options)`  
プロットデータ（曲線）を指定したときは，曲線上の点に矢じりをつける。  
曲線には向きがあり，それによって矢じりの向きが決まる。  
"Invert(曲線名)" とすると反対向きの矢じりになる。  
曲線の向きとは，曲線を描くときの順序で，プロットデータの順序でもある。  
位置比は曲線上の位置を表す比率（0 から 1）。  
  
曲線cr1 上の点 A の位置比が 0.6 のとき  
```  
Arrowhead(A,"cr1");//cr1上の点Aに矢じりを描く  
Arrowhead(0.6,"cr1",[2,1,0.5,"Color=red"]);//cr1上の位置比0.6の点に大きさ2,開き角1,位置0.5に赤色の矢じりを描く  
Arrowhead(1,"cr1");//cr1上の位置比1の点に矢じりを描く  
Arrowhead(1,"Invert(cr1)",["Line=y"]);//cr1とは逆向きの曲線上の位置比1のところに線だけの矢じりを描く  
```  
  
#### `Lineplot（name , 2 点のリスト , options)`  
2 点のリストで示された点を結ぶ直線を描く。  
2 点のリストは座標または幾何要素の名前で与える。  
options は次の通り。  
線種"dr, n", "da,m,n" , "do,m,n"  
"+"半直線を描く。  
"dr" , "da" , "do" と "+" はリストにして両方指定することができる。  
点のリストが，座標ではなく幾何要素名のリストの場合は，name は省略できる。  
いくつか例を示す。  
【例】(0,0)と(1,2)を結ぶ直線を書く  
```  
Lineplot("1",[[0,0],[1,2]])  
```  
Cinderella の描画ツールで 2 点 A,B をとっておき，直線 AB を引く  
```  
Lineplot([A,B]);  
```  
option の働きの例  
```  
Lineplot([A,B],["dr,0.5","+"]); //A を端点とする半直線を引く  
Lineplot([C,D],["dr,2"]); //直線 CD を太さ 2 で描く  
Lineplot([E,F],["da"]); //直線 EF を破線で描く  
Lineplot([G,H],["do"]); //直線 GH を点線で描く  
```  
  
#### `Listplot（name , 点のリスト , options)`  
点のリストで示された点を結ぶ。  
点のリストは座標または幾何要素名のリストで与える。点が，座標ではなく幾何要素名の場合は，name は省略可  
プロットデータの名前は，"sg" に引数の name を付加したものとなる。  
options は次の通り。  
線種 "dr, n", "da,m,n" , "do,m,n"  
トリミング :"Cutend=m" または "Cutend=[m,n]"  
数のときは両端を m だけカットする。リストのときは始点を m, 終点を n だけカットする。m が負のときは延長する。  
  
options の使用例  
```  
Listplot([A,B]); //線分 AB を描く。太さは 初期設定。  
Listplot([C,D],["dr,2"]); //線分 CD を描く。太さ 2  
Listplot([E,F],["da"]); //線分 EF を破線で描く  
Listplot([G,H],["da,3,1"]); //線分 GH を破線で描く。線を長く  
Listplot([K,L],["da,1,3"]); //線分 KL を破線で描く。間隔を空ける  
Listplot([M,N],["do"]); //線分 MN を点線で描く。  
Listplot([O,P],["do,3"]); //線分 OP を点線で描く。間隔を空ける  
Listplot([Q,R],["do,3,3"]); //線分 QR を点線で描く。間隔を空けて太く  
```  
  
【例】三角形を描く。  
Cinderella の作図ツールで三角形 ABC を描いておく。あるいは，単に 3 点 A,B,C をとるだけでもよい。  
```  
Addax(0);  
Listplot([A,B,C,A]);  
```  
  
点の位置は座標で指定してもよい。  
その場合は name が必要。  
```  
Listplot("1",[[0,0],[2,0],[1,2],[0,0]]);//(0,0),(2,0),(1,2),(0,0)を結んだ線を描画する。  
```  
  
【例】有限フーリエ級数展開  
  
\frac{\pi}{2}+\sum_{n=0}^{30}\frac{1-\left(-1\right)^n}{n} \sin nx  
  
次のように Cindyscript で関数を定義し，プロットデータ pd を作って引数に渡す。  
```  
f(x):=(  
s=pi/2;  
repeat(30,n,s=s+(1-(-1)|^n)/n*sin(n*x));  
);  
pd=apply(0..200,t,  
x=-2*pi+t*4*pi/200;  
[x,f(x)];  
);  
Listplot("1",pd);//リストであるpdに格納されている点を結ぶ  
Expr([[-2*pi,-0.5],"s","-2\pi",[-pi,-0.5],"s","-\pi",[pi,-0.5],"s",  
"\pi",[2*pi,-0.5],"s","2\pi",[0,pi],"w2","\pi"]);  
```  
  
リストの長さには制限がある。たとえば，タートルグラフィクスを用いたシェルピンスキーのギャスケットで  
は 200 くらいずつのリストに分割する。   
  
#### `Framedata(name , リスト,options)`  
矩形を描く  
リストの形は 2 通り。  
その 1：[中心 , 横 , 縦] で，矩形を描く。横，縦は中心からの距離。  
その 2：2 点のリスト。点が座標でなく名称のときは name は省略できる。  
点の座標は点の名前でもよい。点を座標で与える場合は name は省略できない。  
リストを省略した場合は，描画範囲と同一の矩形を描く。  
その 2 のタイプでは，option として，"center" または "corner" がある。"center" のときは，中心と対角点（ 初期設定），"corner" のときは 2 点を対角点として解釈する。  
以下にいくつか例を示す  
```  
Framedata("1");//描画範囲 (SW,NE) と同一の矩形を描く  
Framedata("2",[[0,0],2,2]);// 原点を中心とする縦横幅 4 の正方形を描く  
Framedata("3",[A,1.5,1.2]);// 点 A を中心とする横 3，縦 2.4 の矩形を描く。  
Framedata([B,C]);// 点 B を中心, 点 C を頂点とする矩形を描く。  
Framedata([D,E],["corner"]);// 点 D,E を対角点とする矩形を描く。  
```  
矩形の角を丸めたい場合は，`Framedata()` ではなく，`Ovaldata()` を使うとよい。  
  
#### `Polygonplot(name , 点リスト , 整数,options)`  
2 点を半径とする円に内接する正多角形を描く。  
点リストを [A,B] とすると，A を中心とする半径 AB の円周上に点をとって正多角形を描く。ただし円は描かない。A,B は座標でもよい。  
点リストが座標ではなく作図してある点の名称のとき，オプションに"Geo=y" をつけると，頂点の幾何点を作る。幾何点の名称は B に番号を付けたものとなる。整数でない数を指定した場合は，きちんと閉じない折れ線が描かれる。  
  
【例】  
```  
Addax(0);  
Polygonplot("1",[[-4,1],[-4,3]],7);//(-4,1)を中心とし、(-4,3)を通る円に内接する7角形を描く  
Polygonplot("2",[A,B],7);//Aを中心とし、Bを通る円に内接する7角形を描く  
Polygonplot("3",[C,D],7,["Geo=y"]);//Cを中心とし、Dを通る円に内接する7角形を描き、Cinderella上に点を作図する  
```  
  
円に内接する形でなく，与えられた線分 AB を１辺とする正多角形を描くには次のようにする。  
線分 AB は，Cinderella の作図ツールなどで描かれているものとする。ただし，線分でなく，両端の点が与えられているだけでもよい。Cindyscript で点 A,B が複素平面上にあるものとして，多角形の頂点の位置を計算する。  
  
【例】AB を 1 辺とする正五角形を描く。  
```  
n=5;  
pti=[complex(A),complex(B)];  
th=2*pi/n;  
repeat(n-2,s,  
z1=pti_s;  
z2=pti_(s+1);  
z=z2+(z2-z1)*(cos(th)+i*sin(th));  
pti=append(pti,z);  
);  
pt=apply(pti,gauss(#));  
pt=append(pt,A.xy);  
Listplot("1",pt);  
```  
pti は，各頂点に対応する複素数のリスト，pt が各頂点の座標のリストである。  
  
  
### 曲線  
#### `Bezier(名前，節点リスト，制御点リスト，[オプション] )`  
ベジェ曲線を描く  
制御点は，各区間に対して，3 次の場合 2 個，2 次の場合 1 個のリストで与える。  
オプションは  
"Num=n": 節点間の分割数（分点数 −1）を指定できる。 ベジェ曲線とスプライン曲線の関数は節点間が短  
い場合が多いので初期設定は 10 になっている。Plotdata() などと違い，大きい数（200 など）を指定すると，  
全体の分割数が増大して描画時間がかかるようになってしまうので注意。  
  
```  
Ketinit();  
//点ABCを定義  
Putpoint("A",[-5,0],[A.x,A.y]);  
Putpoint("B",[5,0],[B.x,B.y]);  
Putpoint("C",[2,3],[C.x,C.y]);  
//節点がA,Bで制御点がCの2次のベジェ曲線を描画する。分割数は200。  
Bezier("1",[A,B],[C],["Num=200"]);  
  
Windispg();  
```  
  
【例】3次ベジェ曲線(節点がA,Bで制御点がC,D)  
```  
Bezier("2",[A,B],[C,D]);  
```  
  
【例】節点を増やす。2次と3次。(A-B間はDを制御点とする2次のベジェ曲線,B-C間はE,Fを制御点とする3次のベジェ曲線)  
```  
Bezier("3",[A,B,C],[[D],[E,F]]);  
```  
  
【例】全て同じ次数の場合(全て3次で、A-B間,B-C間,C-D間の制御点はそれぞれEとF,GとH,KとL)  
```  
Bezier("4",[A,B,C,D],[E,F,G,H,K,L]);  
```  
  
オプションの例  
```  
Bezier("5",[A,B,C],[[D],[E,F]],["Num=3"]);//分割数を3に指定  
Bezier("6",[A,B,C],[[D],[E,F]],["Num=40","da"]);//分割数を40に指定し、破線で描く  
Bezier("1",[A,B,C,D],[E,F,G,H,K,L],["Num=[2,3,4]"]);//A-B間の分割数を2に、B-C間の分割数を3に、C-D間の分割数を4にする。  
```  
  
#### `Beziersmooth(名前，節点リスト，[オプション] )`  
節点間を3次ベジェ曲線でスムーズに結んだ曲線を描く  
節点をはさむ制御点は１直線上にとる（したがって，１つは半自由点で，直線上しか動けない）。制御点は自動的に配置される。その後，節点や制御点を動かして，描きたいものにする。  
  
```  
Ketinit();  
  
Setketcindyjs(["Figure=y"]);  
//点A,B,C,Dをそれぞれ定義  
Putpoint("A",[-5,0],[A.x,A.y]);  
Putpoint("B",[5,0],[B.x,B.y]);  
Putpoint("C",[2,3],[C.x,C.y]);  
Putpoint("D",[2,-2],[D.x,D.y]);  
Beziersmooth("1",[A,B,C,D],["num=2000"]);//A-B,B-C,C-D間を3次ベジェ曲線でスムーズに結んだ曲線を描く(節点を挟む制御点は一直線上)  
Pointdata("1",[A,B,C,D],["Size=2"]);  
Letter([A,"n2","A",B,"n2","B",C,"n2","C",D,"n2","D"]);  
  
Windispg();  
```  
  
#### `Beziersym(名前，節点リスト，[オプション] )`  
節点間を 3 次ベジェ曲線でスムーズに結んだ曲線を描く  
節点をはさむ制御点は節点に関し対称（片方は表示されず，動かせない）。制御点は自動的に配置される。その後，節点や制御点を動かして描きたいものにする。  
  
【例】  
```  
Ketinit();  
  
Putpoint("A",[-5,0],[A.x,A.y]);  
Putpoint("B",[5,0],[B.x,B.y]);  
Putpoint("C",[2,3],[C.x,C.y]);  
Putpoint("D",[2,-2],[D.x,D.y]);  
Beziersym("1",[A,B,C,D],["num=2000"]);//A-B,B-C,C-D間を3次ベジェ曲線でスムーズに結んだ曲線を描く(節点を挟む制御点は節点に関し対称)  
Pointdata("1",[A,B,C,D],["Size=2"]);  
Letter([A,"n2","A",B,"n2","B",C,"n2","C",D,"n2","D"]);  
  
Windispg();  
```  
  
#### `Mkbeziercrv(名前, [節点リスト, 制御点リスト] のリスト,options )`  
複数のベジェ曲線を描く  
 [節点リスト, 制御点リスト] が１つの場合は，Bezier() と同じ。  
【例】  
```  
Ketinit();  
addax(0);  
  
Mkbeziercrv("5",[[[A,B,C],[[D],[E,F]]],[[G,H,K,L],[[M],[N,O],[P]]]],["Num=200"]);//A-B間を点Dで制御、B-C間を点E,Fで制御するベジェ曲線、G-H間を点Mで制御、H-K間を点N,Oで制御、K-L間を点Pで制御するベジェ曲線をそれぞれ描く(PD名は前者がbz51、後者がbz52となる。bz+name+指定した順番となる。)  
Letter([A,"s2","A"]);  
Letter([B,"s2","B"]);  
Letter([C,"s2","C"]);  
Letter([D,"n2","D"]);  
Letter([E,"n2","E"]);  
Letter([F,"n2","F"]);  
Letter([G,"s2","G"]);  
Letter([H,"s2","H"]);  
Letter([K,"s2","K"]);  
Letter([L,"s2","L"]);  
Letter([M,"n2","M"]);  
Letter([N,"n2","N"]);  
Letter([O,"n2","O"]);  
Letter([P,"n2","P"]);  
Pointdata("1",[A,B,C,D,E,F,G,H,K,L,M,N,O,P],["Size=2"]);  
Windispg();  
```  
  
#### `Mkbezierptcrv( ptlist，[オプション] )`  
ベジェ曲線を描く  
**制御点は，自動的に配置される。**  
ptlistには節点を記載する。  
複数の場合は` [ ptlist1, ptlist2.... ]`  
名前は，a から順に自動的につける。  (プロットデータ名はbza,bzb,bzc...となる)  
オプション  
"Deg=..." 次数指定ができる。（初期設定は 3 次）  
"Num=..." 各区間の区間数（分点数 −1）を指定できる。（初期設定は 10）  
  
【例】  
```  
Ketinit();  
Putpoint("A",[-5,0],[A.x,A.y]);  
Putpoint("B",[5,0],[B.x,B.y]);  
Putpoint("C",[2,3],[C.x,C.y]);  
Mkbezierptcrv([A,B,C]);//A-B,B-C間を3次のベジェ曲線で結ぶ。制御点は自動生成。  
  
Windispg();  
```  
  
その他の例  
【例】制御点の数を1つにする  
```  
Mkbezierptcrv([A,B,C],["Deg=2"]);//A-B,B-C間を2次のベジェ曲線で結ぶ。制御点は自動生成。次元数に応じてDeg=の値を書き換える。3次元なら3でよい。  
```  
【例】節点の数を増やす  
```  
Mkbezierptcrv([A,B,C,D,E,F,X]);//点の数に応じてptlistのリストの長さを変化させる。  
```  
【例】複数の場合  
```  
Mkbezierptcrv([[A,B,C],[D,E,F,G]]);//A-B,B-C間とD-E,E-F,F-G間をそれぞれ3次のベジェ曲線で結ぶ。  
```  
  
#### `Bspline(名前，制御点リスト，[オプション] )`  
2 次 B-spline 曲線を描く  
節点は自動的に計算され，表示されない  
  
【例】  
```  
Bspline("1",[A,B,C,D,E])//A-B,B-C,C-D,D-E間をBsplineで結ぶ。点の数に応じてリストの中身を変更すれば良い。  
```  
これは， `Bezier("1",[A,(B+C)/2,(C+D)/2,E],[B,C,D])` と同じ。**曲線の名前が `bz1` ではなく `bzb1` となる。**   
通常の B-spline 曲線の端の制御点の代わりに，端点を動かせるようにしている。  
  
【例】  
```  
Bspline("1",[A,B,C,D,A]);  //A-B,B-C,C-D,D-A間をBsplineで結ぶ。点の数に応じてリストの中身を変更すれば良い。  
```  
リストの最初と最後が同じ場合は閉曲線になる。  
`Bezier("1",[(D+A)/2,(A+B)/2,(B+C)/2,(C+D)/2,(D+A)/2],[A,B,C,D]);`と同じ。  
  
#### `CRspline(名前，節点リスト，[オプション] )`  
単独の Catmull-Rom スプライン曲線を描く  
自由点は，節点のみで，制御点は節点から作られ移動はできない。  
オプションに，通常のオプションのほか，次が使える。  
size ->n:画面上での線の太さを指定する。  
  
【例】  
`CRspline("1",[A,B,C,D]);`  
  
#### `Ospline(名前，制御点リスト，[オプション] )`  
大島の spline 曲線を描く  
制御点を通るスプライン曲線を描く  
リストの最初と最後が同じ場合は閉曲線になる。  
  
#### `Circledata(name, リスト,options)`  
円または多角形を描く。  
中心の点と，円周上の 1 点（か半径），または 3 点をリストで与えて円を描く。  
中心と円周上の点を，座標ではなく幾何要素名で指定する場合は name は省略可。  
options は以下のものをリストで与える。省略した場合は実線で円が描かれる。   
`"Rng=[θ1, θ2]"` 角 θ1 から θ2 の範囲の弧を描く。角は弧度法で与える。  
`"Num=分割数"` 円を描くときの分割数。値が小さい場合は多角形になる。  
線種 `"dr, n"`, `"da,m,n"` , `"do,m,n"`  
  
【例】  
```  
Ketinit();  
addax(0);  
Circledata("1",[[0,0],[2,0]],["dr,2"]);//中心が(0,0)で(2,0)を通る円を太さ2の実線で描画する。  
  
Windispg();  
```  
  
【例】  
```  
Circledata([A,B]);//A中心、半径ABの円  
Circledata("1",[A,A+[2,0]]);A中心、半径2の円  
Circledata([A,B,C]);//点A,B,Cを通る円。プロットデータ名は"crABC"  
```  
  
`Circledata([A,B,C]);` で，3 点 A,B,C を通る円を描いたとき，できた円の中心は `Pointdata("1",[crABCcenter]);`で作図できる。  
  
【例】扇型を描く  
```  
Ketinit();  
addax(0);  
Putpoint("A",[0,0],[A.x,A.y]);  
Putpoint("B",[3,0],[B.x,B.y]);  
Circledata([A,B],["Rng=[0,pi/3]"]);//A中心、半径ABの円のうち、角度が0からpi/3までの部分だけ描画する。角度を変えたいときは、Rng=のリストの中の値を変更すれば良い。  
Pointdata("1",[A,B],["Size=2"]);  
Letter([A,"s2","A",B,"s2","B"]);  
  
Windispg();  
```  
【例】円を太く描く  
```  
Circledata([A,B],["dr,3"]);//A中心、半径ABの円を太さ3の実線で描く。  
```  
  
【例】正六角形を描く。  
```  
Circledata("2",[A,B],["Num=6"]);////A中心、半径ABの円に**内接する**正六角形を描く。頂点の数を変更するならNum=の数字を変えれば良い。正10角形を描くならNum=10とすれば良い。  
```  
  
#### `Ellipseplot(name, 点リスト , 定義域, options)`  
焦点と通る点を与えて楕円を描く。  
点リストで 2 つの焦点と通る点を与える。点は Cinderella の幾何点が使える。  
また，通る点のかわりに，焦点からの距離の和を実数で与えることもできる。  
  
【例】点 A,B を焦点とする楕円を描く。  
```  
Ketinit();  
addax(0);  
Putpoint("A",[0,0],[A.x,A.y]);  
Putpoint("B",[3,0],[B.x,B.y]);  
Putpoint("C",[4,2],[C.x,C.y]);  
Ellipseplot("1",[A,B,C],["do,2,2"]);// 点 C を通る楕円を描く。(点線)  
Ellipseplot("2",[A,B,4],["da,2"]); //焦点からの距離の和が 4 である楕円を描く。  
Ellipseplot("3",[A,B,C],"[0,pi]",["dr,1"]); //楕円の半分を描く。(角度が0からpiまでを描いている。)  
  
Pointdata("1",[A,B,C],["Size=2"]);  
Letter([A,"s2","A",B,"s2","B",C,"s2","C"]);  
  
Windispg();  
```  
  
#### `Hyperbolaplot(name, 点リスト , 定義域, options)`  
焦点と通る点を与えて双曲線を描く。  
#### 説明  
点リストで 2 つの焦点と通る点を与える。点は Cinderella の幾何点が使える。  
また，通る点のかわりに，焦点からの距離の差を実数で与えることもできる。  
option として，`"Asy=線種"`を与えると，漸近線を指定した線種で表示する。 初期設定では漸近線は非表示。  
  
【例】  
```  
Ketinit();  
  
Putpoint("A",[0,0],[A.x,A.y]);  
Putpoint("B",[3,0],[B.x,B.y]);  
Putpoint("C",[4,2],[C.x,C.y]);  
Hyperbolaplot("1",[A,B,C]); //点A,Bを焦点とし、Cを通る双曲線を描く。  
Pointdata("1",[A,B,C],["Size=2"]);  
Letter([A,"s2","A",B,"s2","B",C,"s2","C"]);  
Windispg();  
```  
  
【例】  
```  
Hyperbolaplot("1",[A,B,2]);//点A,Bを焦点とし、焦点からの距離の差が 2 の双曲線を描く。  
```  
【例】  
```  
Hyperbolaplot("1",[A,B,C],["Asy=do"]);//漸近線を点線で描く。  
```  
  
#### `Parabolaplot(name，点リスト , 定義域, options)`  
点リスト [A,B,C] で示された焦点，準線で決まる放物線を描く。  
焦点 A と準線 BC で決定する放物線を描く。  
実際には，2 次関数  y = x^2  のグラフを回転・平行移動して描いており，定義域は，y = x^2 での定義域と考えてよい。定義域は省略することもできる。省略したときの初期値は [-5,5]  
  
【例】点 A を焦点，直線 BC を準線とする放物線を描く  
  
```  
Ketinit();  
  
Putpoint("A",[0,0],[A.x,A.y]);  
Putpoint("B",[3,0],[B.x,B.y]);  
Putpoint("C",[4,2],[C.x,C.y]);  
Parabolaplot("1",[A,B,C],["Num=200"]);//焦点A,準線が直線BCである放物線を描画する。  
Pointdata("1",[A,B,C],["Size=2"]);  
Letter([A,"e2","A",B,"s2","B",C,"s2","C"]);  
Windispg();  
```  
  
#### `Ovaldata(name, 点リスト,options)`  
角を丸くした矩形を描く  
中心と対角の１点を指定し，角を丸くした矩形を描く  
options は，角の落とし具合と線種など。 数字が大きいほど丸くなる。数字で与える。  
```  
Ketinit();  
  
Putpoint("A",[-5,0],[A.x,A.y]);  
Putpoint("B",[5,0],[B.x,B.y]);  
Putpoint("C",[2,3],[C.x,C.y]);  
Putpoint("D",[2,-2],[D.x,D.y]);  
Putpoint("E",[-4,0],[E.x,E.y]);  
Putpoint("F",[2,1],[F.x,F.y]);  
Putpoint("G",[3,-3],[G.x,G.y]);  
Putpoint("H",[4,-2],[H.x,H.y]);  
Ovaldata("1", [A,B]);//角をちょっと丸めた長方形を描く  
Ovaldata("2", [C,D],[0]);//角を丸めない長方形を描く  
Ovaldata("3", [E,F],[1,"dr,3"]);//角を丸めた長方形を太さ3の実線で描く。  
Ovaldata("4", [G,H],[1.5,"da"]);//角を丸めた長方形を破線で描く。  
  
Windispg();  
```  
  
### 関数のグラフ  
#### `Plotdata(name , 式 , 変数と定義域 , options)`  
関数のグラフを描く。プロットデータの名前は，grである  
式で表された関数のグラフを，指定された定義域で描く。  
式，定義域は " " でくくって文字列とする。定義域は x=に続いてリストで指定。  
options は次の通り。  
  
線種 "dr, n", "da,m,n" , "do,m,n"  
"Num=数値" 描画時の分割数  
"Dis=数値" 値が指定数値以上ジャンプする場合は不連続点とみなす。  
"Exc=数値リスト リストで示された点は除外する。  
"Exc=関数" 関数の零点は除外する。  
"Color=RGB" 色指定。RGB は CMYK でもよい。  
  
【例】3 次関数 f(x) = x^3+2x^2+2x+1  のグラフを定義域指定なしで描く。  
```  
Ketinit();  
  
Plotdata("1","x^3+2*x^2+2*x+1","x",["Num=200"]);//f(x) = x^3+2x^2+2x+1 のグラフを描く。関数を変えたいなら式のところを変えれば良い。掛け算は省略できず、*記号が必要である。  
  
Windispg();  
```  
  
【例】三角関数 f(x) = cos x のグラフを定義域 0 < x < 2piで描く。  
```  
Ketinit();  
  
Plotdata("3","cos(x)","x=[0,2*pi]");// f(x) = cos x のグラフを0 < x < 2piで描く。範囲を変えるなら"x="のところのリストを変更すれば良い。0から4なら[0,4]で良い。  
  
Windispg();  
```  
  
【例】Disオプションで不連続点を結ばない。  
```  
Plotdata("3","sin(x)/sqrt(sin(x)^2)","x",["Dis=1","Num=2000"]);//sin(x)/sqrt(sin(x)^2)のグラフを描き、値が1以上ジャンプしてたら不連続点とみなし、その点を結ばない。  
```  
  
#### `Implicitplot(name, 式,x の定義域,y の定義域, options)`  
陰関数のグラフを描く。  
陰関数の式を与えてグラフを描く。式，定義域とも文字列。  
options は,"Wait=n" が指定できる。Wait の初期値は 10。  
<!-- "r","m"に関しては，オプションなしのとき  
1. データファイルがなければ，新しく作る  
1. データファイルが既にあればそれを読み込む  
"m"のとき，強制的にデータファイルを作り直す。  
"r" のとき，すでにあるデータファイルを読み込む。   -->  
  
【例】(x^2+y^2)^2-2*2.5^2*(x^2-y^2)-(3^4-2.5^4)=0で表される曲線を描く  
```  
Ketinit();  
  
Implicitplot("1","(x^2+y^2)^2-2*2.5^2*(x^2-y^2)-(3^4-2.5^4)=0","x","y");(x^2+y^2)^2-2*2.5^2*(x^2-y^2)-(3^4-2.5^4)=0で表される曲線を、変数をx,yとして描く。  
  
Windispg();  
```  
  
#### `Paramplot(name , 式 , 変数と定義域,options)`  
媒介変数表示の曲線を描く。  
式は""でくくった媒介変数表示のリストで与える。  
定義域も " " でくくって文字列とし，`t=`に続いてリストで指定する。  
options は線種が有効  
  
【例】リサージュ図形を描く。(リサージュ図形は[cos(a*t),sin(b*t)]で表される。)  
```  
Ketinit();  
  
Paramplot("1","[cos(5*t),sin(7*t)]","t=[0,2*pi]",["Num=2000"]);//[cos(a*t),sin(b*t)]で表される媒介変数表示のグラフを0<t<2piの範囲で描く。式を変えるなら2番目の引数(数式が書いてあるリスト)、範囲を変えるなら"t="を変える。媒介変数はtでなくてもいい。そのときは、範囲をその変数で定義すること。(媒介変数がsなら範囲は"s=[-1,1]"などとする。)  
  
Windispg();  
```  
  
【例】太線にする。  
```  
Paramplot("2","[2*cos(t),sin(t)]","t=[0,2*pi]",["dr,2"]);//2*cos(t),sin(t)で表されるグラフを0<t<2piの範囲で太さ2の実線で描く。  
```  
  
#### `Polarplot(name , 式 , 変数と定義域,options)`  
極座標表示 $$r = f(\theta)$$ の曲線を描く。  
  
【例】アルキメデスの螺旋を描く。  
```  
Ketinit();  
  
Polarplot("1","t/5","t=[0,6*pi]",["Num=2000"]);//r=t/5で表される極座標表示の曲線をtが0から6piまで描く。  
  
Windispg();  
```  
  
  
#### `Periodfun(定義式, 周期,options)`  
周期関数のグラフを描く。戻り値は Maxima 形式の式と period のリスト。  
周期関数の式を定義してグラフを描く。定義式は，関数式（文字列），区間，分割数のリスト。  
この関数固有のオプションは `"Con="` で，不連続点を線で結ぶか否かと，その時の色。初期設定は破線。結ばない場合は `"Con=n"`，色指定は線種に続いてコンマで区切って指定する。たとえば，`"Con=do,Color=red"`。  
周期（描画回数）は，数またはリストで指定する。周期が m のとき，2m+1 周期分描かれる。  
注意）関数は左右対称な定義域 [-a,a] で定義すること。  
  
【例】矩形関数のグラフを描く。  
```  
Ketinit();  
addax(0);  
defL=["0",[-1,0],1,"1",[0,1],1];  
Periodfun(defL, 2,["Con=dr,3","dr,3"]);  
  
repeat(11,i,start -> -5,  
Listplot(text(i+10),[[i,-1],[i,2]],["da,2,2"]);  
);  
  
  
Windispg();  
```  
  
#### `Tangentplot(name , PD , 位置 , options)`  
接線を描く。プロットデータの名前は，`lntn`  
曲線 PD の指定した位置での接線を描く。位置は `"x=n"` で指定する。  
  
【例】y=sin xのx=1での接線を描く。  
```  
Ketinit();  
  
Plotdata("1","sin(x)","x",["Num=200"]);//y=sin (x) のグラフを描くプロットデータ名はgr1となる。  
Tangentplot("1","gr1","x=1");//y=sin xのグラフ(gr1と定義した)のx=1での接線を描く。プロットデータ名はlntn1となる。  
  
Windispg();  
```  
  
### 文字  
  
#### `Expr([座標 , 位置 , 文字列],option)`  
TEX 記法の文字列を与えて数式を書く。  
Letter で文字列の前後に$ $をおくのと同じ。  
導関数の記号は，シングルクォートを用いる。  
複数の箇所に文字を書く場合は，Letter() と同様，引数をリストにして与える。  
option は サイズの倍率 ("Size=", Cinderra のみ) と色 ("Color=")。  
  
【例】f(x)=\sin xとその導関数f'(x)=\cos xのグラフと式を図に描き込む。  
```  
Ketinit();  
  
Plotdata("1","sin(x)","x",["Num=200","dr,2"]);  
Plotdata("2","cos(x)","x",["Num=200"]);  
Expr([[0.5,1.5],"e","f(x)=\sin x",[3,1],"e","f'(x)=\cos x"]);//点(0.5,1.5)の右側(e)に"f(x)=\sin x"を、点(3,1)の右側に"f'(x)=\cos x"をそれぞれTeX書式で書き込む。  
Windispg();  
```  
  
#### `Exprrot(座標 , 向き , 移動，文字列，option)`  
\TeX 記法の文字列を傾けた数式を書く  
「座標」の位置に，指定された向きで数式を書く。  
向きはベクトルで与える。  
移動（オプション）は `t` (接線方向の移動) ， `n` (法線方向の移動) ，反転 (`r`)。  
座標，向きとも，Cinderella で作図した幾何点を用いることができる。  
option は サイズの倍率 (`”Size=”`, Cinderra のみ) と色 (`”Color=”`)。  
  
【例】  
```  
Ketinit();  
Exprrot([3,2],[4,1],"t0n1","\sum\limits_{r=0}^{n} {}_{n}C_{r}");//[3,2]に方向ベクトル[4,1]の向きでtex形式で\sum\limits_{r=0}^{n} {}_{n}C_{r}と表される文字列を書く  
Windispg();  
```  
  
  
  
#### `Letter([位置, 方向, 文字列],option)`  
文字列を表示する  
「位置（座標）」と方向で指定された場所に文字を書き込む。  
位置（座標）は点の名前で指定することもできる。  
場所は上下左右を東西南北で表し， `n/s/w/e/c` の方向で表す。`c` は中央。  
指定位置からの距離を，数値で与えることもでき，`e2, e3` は e より少し離して置く。  
複数の文字列をリストの形にして渡すことができる。  
注）導関数の記号 ′ は，数式モード（$ ではさむ）で ′（シングルクォート）を用いる。  
option は サイズの倍率 (`"Size="`, Cinderra のみ) と色 `("Color=")`。  
`文字列が一つのときは，  
`Letter(位置, 方向, 文字列,option)  `  
にできる。  
  
【例】座標 (3,2) の右上(ne)にＰを表示  
```  
Letter([[3,2] ,"ne","P"]);  
```  
  
【例】点Ｃを中央(c)としてＣという文字列を表示  
```  
Letter([C ,"c", "C"]);  
```  
  
【例】点Aの上(s)に数式を表示  
```  
Letter([E,"s","$ f(x)=\sinh x $"]);//点Aの上(s)にTeX形式で"f(x)=\sinh x "を表示  
```  
#### `Letterrot(座標, 方向ベクトル, 移動, 文字列, option)`  
文字列を回転して表示する  
座標で示された位置に，方向ベクトルで指定された向きに回転して文字を書き込む。  
移動（オプション）は `t` (指定した方向ベクトルと垂直方向に移動) ， `n` (指定した方向ベクトルと同じ向きに移動) ，反転 (`r`)。  
option は サイズの倍率 (`”Size=”`, Cinderra のみ) と色 (`”Color=”`)。  
  
【例】  
```  
Ketinit();  
Letterrot([3,2],[4,1],"t0n1","Text");//[3,2]に方向ベクトル[4,1]の向きで指定した方向ベクトルと同じ向きに1動かした場所にTextという文字列を出力する  
Windispg();  
```  
  
  
### マーキング  
#### `Anglemark(name, 点リスト , options)`  
点リストで示された角に弧の形状の角の印をつける。  
`Listplot()` などと同様，点リストが点名の場合は name は省略できる。弧を描かず文字だけを入れる場合はoptions に "nodisp" を指定する。  
options は次の通り。  
数値角の印の大きさ。 初期設定は１  
線種`"dr, n", "da,m,n" , "do,m,n"  `  
`"Expr=文字"` または`"Letter=文字"` : 文字を入れる  
`"Expr=位置 , 文字"`: 位置を指定して文字を入れる。位置は頂点からの距離。  
  
【例】\angle ABC に印をつけて、文字を書き込む  
```  
Ketinit();  
Addax(0);  
Putpoint("A",[-2,-1],[A.x,A.y]);  
Putpoint("B",[1,3],[B.x,B.y]);  
Putpoint("C",[2,-1],[C.x,C.y]);  
Listplot("1",[A,B,C],["dr,2"]);  
Anglemark([A,B,C],[2,"dr,2","Expr=1.2,\theta"]);  
Letter(A,"e2","A");  
Letter(B,"w2","B");  
Letter(C,"n2","C");  
Windispg();  
```  
  
#### `Paramark(name, 点リスト , options)`  
点リストで示された角に平行四辺形の形状の角の印をつける。  
`Listplot()` などと同様，点リストが点名の場合は name は省略できる。  
options は次の通り。  
数値角の印の大きさ。 初期設定は 1  
線種`”dr, n”, ”da,m,n” , ”do,m,n”`  
`”Expr=文字”`: 文字を入れる  
`”Expr=位置 , 文字”`: 位置を指定して文字を入れる。位置は頂点からの距離。  
  
`Anglemark()`とほとんど同じ  
  
#### `Bowdata(name, 点リスト , options)`  
弓形を描く  
点リストで与えられた 2 点を結ぶ弓形を描く。  
2 点を反時計回りに回る方向に弓形を描く。  
options は，`[曲がり , 空白サイズ, 文字, 線種]`  
曲がりは弧の曲がり具合の指定。 初期設定は 1  
空白サイズは中央にあける空白の大きさ  
文字は，`"Expr=文字"`  
また，`"Expr=微小移動 , 文字"`で位置を指定して文字を入れる。  
微小移動は t,n。移動量は数字をつける（正負が可）。  
  
```  
Ketinit();  
addax(0);  
Setketcindyjs(["Figure=y"]);  
  
Putpoint("A",[-2,-1],[A.x,A.y]);  
Putpoint("B",[1,3],[B.x,B.y]);  
Putpoint("C",[2,-1],[C.x,C.y]);  
  
Listplot("1",[A,B,C,A],["dr,2"]);  
  
Bowdata("1",[B,A],[1,1,"Expr=C","da"]);//点Bと点Aを反時計回りに回る方向に弓形を破線で描く（曲がり具合1,中央の空白サイズ1）  
Bowdata("2",[C,B],[1,1,"Expr=A","dr,2"]);//点Cと点Bを反時計回りに回る方向に弓形を太さ2の実線で描く（曲がり具合1,中央の空白サイズ1）  
Bowdata("3",[A,C],[1,0,"Expr=t0n4,B","dr,2"]);//点Aと点Cを反時計回りに回る方向に弓形を太さ2の実線で描き、文字の位置を直線ACと平行に0ずらし、直線ACと垂直に4ずらす（曲がり具合1,中央に空白を設けない）  
Letter([A,"w","A",B,"n","B",C,"e","C"]);  
Windispg();  
```  
  
【例】線分と平行の向きに文字を書き込む  
```  
Bowdata([B,A],[1,1,"Exprrot=a"]);//exprrotオプションを指定することで指定した文字列を線分と平行の向きに書き込む  
```  
  
【例】線分と平行の向きに文字を書き込んだうえで文字を直線BAと平行方向にずらす  
```  
Bowdata([B,A],[1,1,"Exprrot=t3n0,a"]);  
```  
  
【例】線分と平行の向きに文字を書き込んだうえで文字を直線BAと垂直方向にずらす  
```  
Bowdata([B,A],[1,1,"Exprrot=t0n3,a"]);  
```  
  
#### `Segmark(name, リスト,options)`  
線分に印をつける  
リストで与えられた 2 点を端点とする線分に印をつける。  
  
オプション  
Type=印の種類 `"seg(,n)","cir","poly(,n)" `   
`Width`：線の幅，`Size`：大きさ  
  
【例】  
```  
Ketinit();  
addax(0);  
  
Putpoint("A",[-1.5,-0.5],[A.x,A.y]);  
Putpoint("B",[0,2.5],[B.x,B.y]);  
Putpoint("C",[1.5,-0.5],[C.x,C.y]);  
Putpoint("D",[2,0],[D.x,D.y]);  
  
Listplot([A,B,C,D,A]);  
Segmark("1",[A,B],["Type=seg,1"]);//線が1本の印  
Segmark("2",[B,C],["Type=seg,2","Width=1.2"]);//線が2本で太さ1.2の印  
Segmark("3",[C,D],["Type=cir"]);//丸印  
Segmark("4",[D,A],["Type=poly,4","Size=1.5"]);//四角形の印で、大きさが1.5  
  
Windispg();  
```  
  
【例】多角形の印の頂点の数を変更する  
```  
Segmark("1",[D,A],["Type=poly,6","Size=1.5"]);//六角形の印を付ける。  
Segmark("2",[A,B],["Type=poly,3","Size=1.5"]);//三角形の印を付ける。  
```  
  
#### `Htickmark([横座標 , 方向 , 文字])`  
横軸に目盛と文字を書く。  
引数は位置（横座標），方向，文字。複数点の情報を `[ ]` 内にまとめて記入できる。方向を省略すると `"s1"`になる。微調整は描画面には反映されないので，PDF にして確認する。目盛の長さは `Setmarklen()` で設定できる。  
  
【例】目盛りの位置を変える  
```  
Ketinit();  
  
Plotdata("1","x^2-1.2","x",["Num=200","dr,2"]);  
Htickmark([1,"n2","1",2,"s2","2",3,"s2","3",4,"s2","4"]);//x座標が1の目盛りを上に、かつ2ずらして書く。2から4は下に2ずつずらして書く。  
Windispg();  
```  
  
【例】リスト処理で目盛りを書く  
```  
Ketinit();  
  
Plotdata("1","sin(x)","x",["Num=200","dr,2"]);  
memori=apply(-5..5,a,[a*pi/2,"s4","\frac{"+text(a)+"\pi}{2}"]);//-5から5の数字に対して[a*pi/2,"s4","\frac{"+text(a)+"\pi}{2}"]という要素を作成する。  
memori=flatten(remove(memori,[[0,"s4","\frac{"+text(0)+"\pi}{2}"]]));//0の部分はいらないので削除し、flatten関数により2次元配列を1次元配列に変換する。  
Htickmark(memori);  
  
Windispg();  
```  
  
#### `Vtickmark([横座標 , 方向 , 文字])`  
`Htickmark`とほとんど同じ。方向の指定を省略すると`w1`が指定される。  
  
#### `Rulerscale(始点 ，横軸目盛 , 縦軸目盛)`  
目盛を打つ  
始点の位置を縦横の起点として目盛りを打つ。目盛はリストで与える。`[”r”,a,b,c,d]` の形式では，`a` から `b` まで `c` 間隔で，倍率 `d` の目盛を打つ。`[”f”,n1,”str”,n2,”str”,・・]`の形式では，`n` と`”str”`がセットで，`n` の位置に`”str”`を書く。ただし，位置は Cinderella の描画面の原点を 0 とする。  
`Listplot()` とともに用いると，座標軸とは異なる線分に目盛を打つことができる。  
`Framedata()` とともに用いると矩形に目盛を打つことができる。  
【例】  
```  
Ketinit();  
Listplot("1",[[5,1],[2,1],[2,3]]);  
Rulerscale([2,1],["r",2,5,1,10],["f",2,"a_1",3,"a_2"]);//横軸を2から5まで1間隔で10倍の目盛りを打つ、縦軸を2の位置にa_1を、3の位置にa_2を書く  
  
Windispg();  
```  
  
## プロットデータの操作  
  
#### `Changestyle(PD リスト, options)`  
描画オプションを変更する  
複数の図形の描画オプションを一括して変更する。  
  
```  
Ketinit();  
opgr=["dr,3"];//変更用にオプションをリストで保存  
Plotdata("1","x^3+2*x^2+2*x+1","x",["Num=200"]);  
Changestyle(["gr1"],opgr);//オプションを保存したリストをoptionsに渡してgr1に反映させる  
  
Windispg();  
```  
  
#### `Invert(PD)`  
プロットデータを逆順にする  
  
#### `Joincrvs(name, プロットデータのリスト, options)`  
隣接する曲線プロットデータ のリストを繋いで 1 本の曲線を作る。  
曲線のリストは隣接する順番で指定する。  
options は線種"dr, n", "da,m,n" , "do,m,n"  
プロットデータ名は"join"  
  
【例】  
```  
Ketinit();  
  
Plotdata("1","x^2","x=[-1,2]",["Num=200"]);//y=x^2のグラフを-1<x<2の範囲で描く  
Plotdata("2","x+2","x=[-1,2]",["Num=200"]);//y=x+2のグラフを-1<x<2の範囲で描く  
Joincrvs("1",["gr1","gr2"]);//y=x^2のグラフの-1<x<2の範囲(gr1)とy=x+2のグラフの-1<x<2の範囲(gr2)をつなげて一つの曲線にする。プロットデータ名はjoin1となる  
Shade(["join1"],["Color=red"]);//join1を赤く塗りつぶす  
  
Windispg();  
```  
  
#### `Partcrv(name, A, B, プロットデータ, options)`  
曲線プロットデータ上の点 A, B の間の部分曲線を描く。  
 2 点 A, B の順序は曲線の向きと同一であること。曲線の向きは，y = f(x) のグラフでは x 座標が増加する向き。  
options は線種"dr, n", "da,m,n" , "do,m,n"  
```  
Ketinit();  
  
Plotdata("1","x^2","x",["Num=200","do"]);  
  
Partcrv("1", [-1,1], [2,4], "gr1",["dr,3"]);//"gr1"の[-1,1]から[2,4]までを太線にする  
  
Windispg();  
```  
  
【例】`Intersectcrvs`を使って動的に交点を求める  
```  
Ketinit();  
Setketcindyjs(["Figure=y"]);  
Putpoint("A",[-2,-1],[A.x,A.y]);  
Putpoint("B",[1,3],[B.x,B.y]);  
  
Lineplot("1",[A,B],["da"]);  
  
Circledata("1",[[0,2],[0,0]],["dr"]);  
tmp=sort(Intersectcrvs("ln1","cr1"));  
print(tmp);  
Intersects=length(tmp);  
if(Intersects>0,  
if(A.x<B.x,  
Partcrv("1",tmp_1,tmp_2,"ln1",["dr,2"]);,  
Partcrv("1",tmp_2,tmp_1,"ln1",["dr,2"]);  
);  
);  
Windispg();  
  
```  
  
#### `Enclosing(name , PD リスト , [開始位置, 交点計算の許容限界 1, 2])`  
複数の曲線から閉曲線を作る。  
開始位置は，最初と最後の曲線の交点が複数あるときに指定する。  
開始点は近くに取ればよい。許容限界は，通常は指定しなくてよい。  
プロットデータ名はen  
  
【例】 y=x+2 とy=x^2のグラフの内側を塗りつぶす。  
```  
Ketinit();  
  
Plotdata("1","x^2","x",["Num=200"]);//y=x^2のグラフを描く  
Plotdata("2","x+2","x",["Num=200"]);//y=x+2のグラフを描く  
  
Enclosing("1",["gr1","gr2"],["nodisp"]);//y=x^2のグラフとy=x+2のグラフから閉曲線を作る  
Shade(["en1"],["Color=red"]);//en1の内部を赤色(red)で塗る  
  
Windispg();  
```  
  
#### `Hatchdata(name , 方向リスト , プロットデータ , options)`  
閉曲線の内部に斜線を引く。  
引数は，曲線名，内部外部のパターンを与える"i"，"o" の文字列，閉曲線を与える曲線と領域の内部を定める  
方向のリストとオプション。  
オプション（カッコ内はデフォルト値）  
　角度 (45)，間隔 (1)，"Max=(20)" 斜線の最大本数，  
"No=点リスト" 点リストの点が選ばれているときは実行しない  
"File=y/m/n (n)" データファイルを作るか  
"Check=点リスト" 点リストの点が変更されていたら，ファイルを作り直す  
  
【例】 y=x+2 とy=x^2のグラフの内側をハッチングする。  
```  
Ketinit();  
  
Plotdata("1","x^2","x",["Num=200"]);  
Plotdata("2","x+2","x",["Num=200"]);  
  
  
Enclosing("1",["gr1","gr2"],["nodisp"]);  
  
Hatchdata("1",["i"],[["en1"]],[-45,1,"dr,0.5"]);//en1の内側(i)に対して-45度に傾けた太さ0.5の実線でハッチングする  
  
Windispg();  
```  
  
#### `Shade(("名前"), プロットデータのリスト , options)`  
閉曲線で囲まれた領域を塗りつぶす。  
第１引数には，閉曲線を与える曲線分のプロットデータ名を並べる。  
デフォルトでは，Joincrvs を使って閉曲線を作っている。ただし，プロットデータのリストに"Invert()"が入っていれば，Enclosing を使う。  
option の Color は，Cinderella の画面上での描画色をリストで与える。濃さを指定したい場合は色名や RGBではなく CMYK にする。  
options には，他に，次のものがある。  
・Enclosing を使うかどうか："Enc=y/n"（初期値は n）  
　　　"Enc=y" のとき，複数の Shade を使うときは，名前をつける。  
・Enclosing のときの開始点，描画色  
・描画領域のトリミング："Trim=y/n" （初期値は n ）  
・TeX への書き出しで，先頭に配置するか："First=y/n" （初期値は n ）  
　　　"First=n" のときは，使われている Gdata の書き出しの直前におく．  
  
【例】 y=x+2 とy=x^2のグラフの内側を塗りつぶす。  
```  
Ketinit();  
  
Plotdata("1","x^2","x",["Num=200"]);//y=x^2のグラフを描く  
Plotdata("2","x+2","x",["Num=200"]);//y=x+2のグラフを描く  
  
Enclosing("1",["gr1","gr2"],["nodisp"]);//y=x^2のグラフとy=x+2のグラフから閉曲線を作る  
Shade(["en1"],["Color=red"]);//en1の内部を赤色(red)で塗る  
  
Windispg();  
```  
  
【例】描画範囲外は塗りつぶさない  
```  
Ketinit();  
addax(0);  
  
Circledata("1",[[0,0],[2,0]],["dr,2"]);  
Shade("1",["cr1"],["color=red","Trim=y"]);//描画範囲以外は塗りつぶさない("Trim=y")  
  
Windispg();  
  
```  
  
#### `Reflectdata(name , プロットデータ , 対称点または対称軸,options)`  
プロットデータの鏡映を作成  
プロットデータを指定された点または軸に関して対称移動する。  
対称点は座標または，点の識別名。ただし，対称点を座標で示すときは要素がひとつのリストにする。  
対称軸はリスト `[ 点 1, 点 2 ]` で指定。  
  
【例】y=x^3と[0,0]-[1,1]間を結ぶ直線に対する鏡映を描く  
```  
Ketinit();  
  
Plotdata("1","x^3","x",["Num=200"]);//y=x^3のグラフを描く。  
  
Reflectdata("1","gr1",[[0,0],[1,1]],["dr,2"]);//gr1を[0,0]-[1,1]間を結ぶ直線で反転させたグラフを描く。  
  
Windispg();  
```  
  
【例】y=x^2とそれを点対称移動させたグラフを描く  
```  
Ketinit();  
  
Plotdata("1","x^2","x",["Num=200"]);//y=x^2のグラフを描く。  
  
Reflectdata("1","gr1",[[0,0]],["dr,2"]);//gr1を点(0,0)に関して点対称移動させた図形を描く。  
  
Windispg();  
```  
  
#### `Rotatedata(name , プロットデータ ，角度 , [中心 , options])`  
プロットデータの位置を回転する  
図形を，中心で示された点の周りに回転する。角度は弧度法で与える  
中心と options はまとめてリストで与える。  
  
【例】y=sin x のグラフを 90度回転する  
```  
Ketinit();  
  
Plotdata("1","sin (x)","x",["Num=200","nodisp"]);//y=sin xのグラフを描く。"nodisp"オプションによってCinderellaの画面上には出力しない  
  
Rotatedata("1","gr1",pi/2,[[0,0]]);//gr1を点(0,0)に対してpi/2(90度)回転させる  
  
Windispg();  
```  
  
#### `Scaledata(name , プロットデータ，x 方向比率 , y 方向比率 , [中心 , options])`  
図形の位置を拡大・縮小する  
図形の位置をプロットデータを用いて指定された比率で拡大・縮小する  
比率は `[x 方向比率 , y 方向比率]` のリストで与えてもよい。  
中心と options はまとめてリストで与える。options は線種  
  
【例】y=sin x のグラフを x軸方向に0.5倍にする。  
```  
Ketinit();  
  
Plotdata("1","sin (x)","x",["Num=200"]);//y=sin xのグラフを描く。  
  
Scaledata("1","gr1",0.5,1,[[0,0],"da"]);//gr1をx軸方向に0.5倍、y軸方向に1倍にしたグラフを破線で描く。  
  
Windispg();  
```  
  
#### `Translatedata(name , プロットデータ , 移動ベクトル , options)`  
プロットデータを平行移動する  
プロットデータを移動ベクトルで示された分だけ平行移動する。  
  
【例】y=x^2とそれをx軸方向に2,y軸方向に3移動させたものを描く。  
```  
Ketinit();  
  
Plotdata("1","x^2","x",["Num=200","dr"]);//y=x^2のグラフを描く。  
  
Translatedata("1","gr1",[2,3],["da"]);//gr1をx軸方向に2,y軸方向に3ずらす  
  
Windispg();  
```  
  
#### `Intersectcrvs(プロットデータ 1, プロットデータ 2)`  
2 曲線の交点リストを取得する。  
オプションとして，共有点があるかどうかを判断するための限界値があるが，通常は使わない。  
【例】  
```  
Ketinit();  
  
Plotdata("1","x^2","x",["Num=200","dr"]);//y=x^2のグラフを描く  
Plotdata("2","x+2","x",["Num=200","dr"]);//y=x+2のグラフを描く  
intscs = Intersectcrvs("gr1", "gr2");//gr1とgr2の交点をすべて求め、リストで格納  
Pointdata("1",intscs_1,["Size=4"]);//gr1とgr2の交点のうち1番目を大きさ4でプロット  
Pointdata("2",intscs_2,["Size=4"]);//gr1とgr2の交点のうち2番目を大きさ4でプロット  
  
Windispg();  
```  
  
#### `Intersectcrvspp(プロットデータ 1, プロットデータ 2)`  
2 曲線の交点のパラメータリストを取得する。  
2 曲線の交点の座標とパラメータのリストを返す。  
Intersectcrvs() との違いは，パラメータがあるかどうかである。  
`Intersectcrvs`の例を`Intersectcrvspp`に変更し、リストをコンソールに表示すると、  
```  
[[[-1,1],9,8],[[2,4],15,14]]  
```  
と表示される。  
このパラメータは曲線を分割した家の何番目に位置するかという意味である。  
[[-1,1],9,8]なら、(-1,1)で交わっていて、1つ目の曲線の9番目と、2つ目の曲線の8番目で交わっているという意味である。小数なら中間の場所である。  
  
#### `Intersectcurves,Intersectcurvespp`  
2 曲線の交点リストを取得する。接点があるとき，時間はかかるがより精密な値を返す。  
引数は`Intersectcrvs,Intersectctvspp`と同じ。ppあり、なりに関しても同じ。  
  
#### `Nearestptcrv(座標, プロットデータ)`  
点に最も近いプロットデータの点を求める  
第1引数の座標に最も近い曲線プロットデータ上の点の座標を返す。  
【例】  
```  
Ketinit();  
Setketcindyjs(["Figure=y"]);  
Putpoint("A",[1,1],[A.x,A.y]);  
  
Plotdata("1","x^2","x",["Num=200","dr"]);//y=x^2のグラフを描く  
  
nrst=Nearestptcrv(A,"gr1");//点Aとgr1の距離が最も近いgr1上の点の座標を取得する  
Pointdata("1",nrst,["Size=4"]);nrstの座標に点を打つ  
Listplot("1",[A,nrst],["da"]);点A-nrst間に線を引く  
  
  
Windispg();  
```  
  
#### `Paramoncrv(点の座標, 曲線の名前)`  
曲線上の点のパラメータ値を返す。  
曲線は折れ線として描かれ，曲線上の各点はこの折れ線の節点を基準としたパラメータ値を持つ。パラメータ値は整数部分が節点の番号，小数部分が節間の位置を表す。  
【例】  
```  
Ketinit();  
Setketcindyjs(["Figure=y"]);//html出力を描画範囲内に限る  
  
Plotdata("1","x^2","x",["Num=200","dr"]);  
Putoncurve("P","gr1",[0,0]);曲線gr1上に点Pを置く(動かせる)  
prm = Paramoncrv(P, "gr1");点Pがgr1上のどこにあるかのパラメータ値をprmに格納  
  
Expr([[1,-1],"e",text(prm)]);//点(1,-1)の右側に文字列化したprmをtex形式で表示  
  
Windispg();  
```  
  
#### `Pointoncrv(点のパラメータ値, PD)`  
曲線上のパラメータ値を持つ点の座標を返す。  
曲線（折れ線）上の節点を基準としたパラメータ値により点の位置が定まる。  
  
#### `Ptcrv(n, プロットデータ)`  
曲線プロットデータの n 番目の節点を返す  
Cindyscript の `PD_n` と同じ  
  
【例】六角形の一部の辺だけ太くする  
```  
Ketinit();  
addax(0);  
  
Circledata("1",[[0,0],[2,0]],["Num=6","dr,1"]);//正六角形を太さ1の実線で描く  
a=Ptcrv(2,cr1);//aをcr1の2番目の節点の座標とする  
b=Ptcrv(4,cr1);//bをcr1の4番目の節点の座標とする  
Partcrv("1",a,b,"cr1",["dr,3"]);//cr1のa-b間を太さ3の実線で描く。  
  
Windispg();  
```  
  
#### `Ptstart(プロットデータ) , Ptend(プロットデータ)`  
プロットデータの最初の点，最後の点を取得する。  
プロットデータの最初の点，最後の点の座標を返す。  
  
#### `Readcsv(path,filename,option)`  
csv ファイルを読む。  
  
【例】以下のファイルを読み込む  
```csv  
1,2,3,4  
5,6,7,8  
```  
  
```  
Ketinit();  
a=Readcsv("path/to/your/file","example.csv");//第1引数のディレクトリにある第2引数のcsvファイルを読み込んで変数aに格納。(2次元配列)  
println(a);//変数aを表示  
Windispg();  
```  
この出力結果は以下のようになる  
```txt  
[[1,2,3,4],[5,6,7,8]]  
```  
  
#### `Readlines(path,filename,option)`  
テキストファイルを 1 行ずつ読む。  
  
【例】以下のファイルを読み込む  
```txt  
abcde  
fghij  
```  
  
```  
Ketinit();  
a=Readlines("path/to/your/file","example.txt");//第1引数のディレクトリにある第2引数のファイルを読み込んで変数aに格納。(配列)  
println(a);  
Windispg();  
```  
この出力結果は以下のようになる  
```txt  
[abcde,fghij]  
```  
  
#### `WriteOutData(ファイル名,PD リスト)`  
外部データに書き出す  
  
【例】x^2のプロットデータを外部ファイルに書き込む  
```  
Ketinit();  
Plotdata("1","x^2","x",["Num=200"]);//x^2のグラフをプロットデータを作成する。  
WriteOutData("gr1.txt",["gr1"]);//gr1をgr1.txtに書き込む  
Windispg();  
```  
  
#### `Extractdata(データ名, 属性)`  
`ReadOutData()` で読み込んだデータに属性をつける。  
  
【例】`WriteOutData`の説明で書き出したファイルを読み込んで`dr,2`属性をつける  
```  
Ketinit();  
ReadOutData("gr1.txt");  
Extractdata("gr1",["dr,2"]);  
  
Windispg();  
```  
  
## 計算  
#### `Derivative(関数式 , 変数 , 値)`  
関数の微分係数を求める  
関数式で与えられた関数の，「変数＝値」における微分係数を求める。  
値は，点の座標を用いることができる。点 A の $$x$$ 座標であれば， `A.x` とする。  
  
【例】$$y=\sin x$$の接線を描く  
```  
Ketinit();  
  
Plotdata("1","sin(x)","x",["Num=200"]);//y=sin xのグラフを描く  
Putoncurve("P","gr1",[0,0]);//gr1上に点Pを置く  
  
D = Derivative("sin(x)","x" , P.x);//xがPのx座標におけるsin xの微分係数を求める  
  
Plotdata("2",Assign("D*(x-P)+sin(P)",["D",D,"P",P.x]),"x",["Num=200"]);//xがPのx座標におけるsin xの微分係数をもちいて接線を描く  
Windispg();  
```  
  
#### `integrate(関数式 , 変数 = 範囲 , options),integrate(PD , 範囲 , options)`  
関数式またはプロットデータで与えられた関数（データ）の数値積分の値を求める。  
  
 options は次の通り。  
”Rule=s”：シンプソン法による。 初期設定は大島ベジェ公式。  
”Num=数値”：分割数の指定。初期値は 100  
  
#### `Inversefun(関数 , 範囲 , 値)`  
関数の逆関数値を求める  
関数は文字列で，関数式もしくは定義された関数名とする。  
指定された範囲の中で逆関数値を求める。存在しない場合は一方の端点を戻り値とし，コンソールに「not found」と表示される。  
数式処理ではなく数値探索のアルゴリズムを使っているので，単調関数でない場合は範囲をできるだけ狭くとるとよい。値が複数ある場合は，小さいほうが返される。  
  
#### 値の取得と入出力  
  
計算値やプロットデータの値を取得したり，R 用とのデータのやりとりをする。  
  
#### `Asin(実数),Acos(実数)`  
逆三角関数の値を求める。  
  
#### `Sqr(実数)`  
平方根を求める。  
  
#### `BBdata(ファイル名,option)`  
TeX 文書において，inputgraphics コマンドで画像を貼り込むときの BB サイズを求める。TeX 処理系のextractbb を用いて画像ファイルから BB データを作り，テキストファイルとして作業ディレクトリに書き出す。これを読んで，コンソールに ingludegarphics のコマンドを書き出す。これをそのままコピーすればよい。  
なお，bb の値は整数値ではなく，高精細の値を小数点以下 2 桁に四捨五入して示される。画像ファイルは，PDF に限らず，PNG，JPG などでもよい。  
option は，幅または高さの指定。  
”w=40mm” で width=40mm が，”h=40mm”で height=40mm が付加される。  
  
#### `Cindyname()`  
作図中のファイル名を取得する。  
  
#### `Crossprod(リスト，リスト)`  
2 つのベクトルの外積を求める。  
  
#### `Dotprod(リスト，リスト)`  
2 つのベクトルの内積を求める。  
  
#### `Findarea(プロットデータ)`  
プロットデータで囲まれる部分の面積を求める。  
  
#### `Findlength(プロットデータ)`  
プロットデータの曲線の長さを求める。  
  
## ファイル操作  
#### `isexists(dir,file)`  
dir に file が存在するか  
返り値は`<bool>`型。  
  
#### `makedir(path,dirname)`  
path に dirname の dir を作成する。  
  
#### `Filelist([mainpath,]dirname)`  
(mainpath にある)dirname のファイルとフォルダの一覧をリストとして返す。  
返り値は`<list>`型。  
  
#### `gethome()`  
ユーザホームを返す。  
返り値は`<string>`型。  
  
#### `ismacosx()`  
macOSかどうかを返す  
返り値は`<bool>`型。  
  
## `iswindows()`  
Windowsかどうかを返す  
返り値は`<bool>`型。  
  
## `islinux()`  
Linuxかどうかを返す  
返り値は`<bool>`型。  
  
## その他  
  
#### `Animationkey([番号リスト, 横位置リスト, 縦位置])`  
アニメーションの PLAY,REV,PAUSE,STOP を作成  
すでにある場合は位置を設定  
オプションのデフォルト値は，[71,72,73,74],[-4,-2,0,2],-6  
  
【例】  
```  
Ketinit();  
Setketcindyjs(["Label=[]","Color=offwhite","Figure=y"]);  
Animationkey();//Animationkeyを作成  
ss=Animationparam(0,2,2*pi);//ssをアニメーション用変数として設定。変域は0から2piで速度は2  
  
Plotdata("1","sin(x)","x=[0,"+text(ss)+"]",["Num=200"]);  
  
Windispg();  
```  
  
#### `Assign(文字列，文字，文字)`  
文字列の中のある文字を他の文字で置き換える  
第１引数の文字列中の第 2 引数の文字を，第 3 引数の文字で置き換える。  
第 3 引数が数値の場合，文字列に変換される。  
第 2 引数と，第 3 引数をリストにして，複数の置き換えをすることができる。  
  
【例】a\*x を 1.3\*x とした文字列を返す。次のいずれも同じ結果になる。  
```  
Assign("x^2+a*x","a","1.3");//"x^2+a*x"の"a"を"1.3"に置換  
Assign("x^2+a*x","a",1.3);  
```  
  
#### `Colorcode(種別 1, 種別 2, カラーコード)`  
種別 1 から種別 2 へカラーコードを変換する。戻り値は変換されたコード。  
種別は，"rgb","cmyk","hsv"のいずれか。  
【例】変換例  
```  
Ketinit();  
col=Colorcode("rgb","cmyk",[1,0,0]);//RGBをCMYKに変換  
println(col);  
col=Colorcode("cmyk","rgb",[0,1,1,0]);//CMYKをRGBに変換  
println(col);  
col=Colorcode("rgb","hsv",[1,0,0]);//RGBをHSVに変換  
println(col);  
Windispg();  
```  
#### `Dqq(文字列)`  
文字列の前後に\"をつける。  
  
Ketinit();  
str="abc";//strに文字列abcを代入  
str2=Dqq(str);変数strの文字列の最初と最後に2重引用符(")を付記してstr2に代入  
  
Windispg();  
  
#### `Factorial(n)`  
n!を計算する。(nは正の整数)  
```  
Ketinit();  
println(Factorial(10));//10の階乗を計算して表示する  
  
Windispg();  
```  
  
#### `Binomial(n,r)`  
2 項係数を計算する。  
```  
Ketinit();  
println(Binomial(15,7));//nCr(15,7)を計算して表示する  
  
Windispg();  
```  
  
#### `Gcd(numberlist)`  
numberlist の最大公約数を計算する。  
```  
Ketinit();  
println(Gcd([24,60]));//24と60の最大公約数を計算して表示する  
  
Windispg();  
```  
  
#### `Norm(ベクトル), Norm(ベクトル 1, ベクトル 2)`  
ベクトル（2 つのベクトルの場合は差の大きさ）の大きさを計算する。  
```  
Ketinit();  
println(Norm([1,1]));//ベクトル(1,1)の大きさを計算して表示する  
println(Norm([1,1],[2,3]));ベクトル(1,1)->(2,3)の大きさを計算して表示する  
Windispg();  
```  
  
#### `Figpdf(option)`  
出力枠サイズの PDF を作る。  
```  
Ketinit();  
  
Listplot("1",[A,B,C,A],["dr,2"]);  
Setparent("test.pdf");  
Figpdf();  
Windispg();  
```  
ここで、Parentボタンを押すと、test.pdfが生成される。  
  
#### `Indexall(str1,str2)`  
文字列 str1 から str2 を検索しその位置をすべて返す  
Cindyscript の indexof() の拡張版。indexof() が最初に見つかった位置を返すのに対し，Indexall() は存在する位置をすべてリストにして返す。  
  
```  
Ketinit();  
  
println(Indexall("The quick brown fox jumps over the lazy dog.","o"));//Indexall関数で文の中のoを探して位置を取得し、それを表示する  
Windispg();  
```  
  
#### `Bracket(str [,bracketpair])`  
文字列 str から bracketpair を検索しその位置と階層レベルのリストを返す。  
bracketpair のデフォルトは”()”。  
```  
Ketinit();  
  
println(Bracket("(abc(de)fg)"));//括弧の位置と深さを取得して表示  
Windispg();  
```  
  
  
#### `Getlevel(str [,poslist または sep,key]);`  
文字列 str から sep の位置と階層レベルのリストを返す。  
sep, key のデフォルトはコンマと ()。sep を与えたとき，poslist は sep の indexlist になる。  
  
#### `Reparse(文字列か文字列のリスト)`  
評価してから実部をとる。  
【例】  
```  
Ketinit();  
println(Reparse("1+sqrt(-1)"));//1+sqrt(-1)を計算して、実部を取る  
Windispg();  
```  
この出力結果は  
```  
1  
```  
となる  
  
#### `Slider(名称，位置 1, 位置 2)`  
スライダを作成する  
名称は ”A-C-B” の形で，端点を A,B，スライダ点を C としたスライダを作る。  
端点 A,B の位置を，位置 1，位置 2 で指定する。単に”C”としたときは端点をとらない。スライダにより取得  
したい値は，点 C の座標（たとえば C.x）を利用する。  
点 A,B,C はあらかじめ作図しておく必要はない。既にある場合はその点を使う。  
【例】  
```  
Ketinit();  
Slider("A-C-B",[-5,-2],[5,-2]);//A(-5,-2),B(5,2)を端点に、点Cをつまみとしてスライダーを作る  
Plotdata("1",Assign("a*x^2",["a",C.x]),"x",["dr,2"]);//C.xの値を元にax^2のグラフを描く。  
Windispg();  
```  
  
#### `Sprintf(実数, 長さ)`  
小数点以下の長さを固定した文字列に変換  
実数を，小数点 n 位までの数とした文字列に変換する  
【例】  
```  
Ketinit();  
println(Sprintf(6.2831,2));//6.2831の小数点以下第2位まで取得して表示  
Windispg();  
```  
  
この出力結果は  
```  
6.28  
```  
となる  
  
#### `Textformat(数，桁数)`  
小数点以下の桁数を指定して数を文字列化する。  
第１引数は数のリストでもよい。数のリストの場合は，戻り値は，対応する数値を指定桁数にした後，リストを文字列化する。Cindyscript の組み込み関数にも，`format()` という同様の関数があるが，`format()` は文字列のリストを返す。  
  
【例】  
```  
Ketinit();  
dt=[1/6,0.5];  
println(Textformat(dt,4));// 1/6,0.5を指定桁数にした後，リストを文字列化して表示する  
println(format(dt,4));//1/6,0.5を指定桁数にして表示する  
Windispg();  
```  
  
この出力結果は  
```  
[0.1667,0.5]  
[0.1667,0.5]  
```  
となる。(両方同じなのは、printlnは文字列型を示す二重引用符を出力しないため)  
  
  
#### `Windispg() または Windisp(データのリスト)`  
定義されているプロットデータを Cinderella 画面に黒線で描く  
Windispg() は，スクリプトの最後に置くことで，出力される部分だけが黒で描かれるので，出力図を確認することができる。ただし，Letter() 関数で表示した点の名称などが Cinderella で作図したラベルと重なって表示されて見にくくなることもある。この関数を実行しなくても出力には影響しない。  
Windisp(データのリスト）は，R から KETCindy 用に出力されたファイルを ReadOutData() 関数で読み込んだときに，必要なプロットデータ列だけを表示するのに用いる。  
ReadOutData(”filename.txt”) でデータを読み込むと，そのデータに含まれるプロットデータ列が，コンソールに  
```  
Outdata of filename.txt : [Gfn,Gdfn,Gh]  
```  
のように表示される。  
このうち，Gfn と Gh だけを表示するのであれば  
```  
Windispg([Gfn,Gh]);  
```  
とする。引数なしで  
```  
Windispg();  
```  
とすればすべてのプロットデータ列が表示される。  
なお，いずれの場合も，作図したプロットデータも同時に表示される。  
作図した図を全てではなく選択して表示する場合は，それらのプロットデータ名をリストにして引数とする。  
たとえば，sg1, gr1, crAB が定義されているとき，  
```  
Windispg(["sg1","gr1"]);  
```  
とすれば，sg1,gr1 のみが表示される。  
**Windispg()は普通、最後の行に書けば問題ない**  
  
#### `Viewtex()`  
TeXのソースファイルを書き出す。引数なし。  
グローバル変数 Fhead で定義したファイル名に ”main” を付加した TEX のソースファイルとバッチファイル（Mac の場合はシェルファイル）を作成する。  
  
#### `Workprocess()`  
作図の経過を取得する  
作図ツールを用いた作図の経過を取得する。  
```  
println(Workproccess());  
```  
とすると，コンソールに作図手順が表示される。  
  
#### `Op(n,list or str)`  
リストまたは文字列から要素を抜き出す  
第 2 引数のリストまたは文字列の n 番目の要素（文字）を返す。  
Cindyscript の アンダーバーの演算子 (list_n , str_n) と同様。  
  
#### `Strsplit(文字列 , 文字)`  
文字列を分解する。  
第１引数の文字列を第 2 引数の文字の位置で分解したリストを返す。  
【例】文字 , で区切って分解する。  
```  
Ketinit();  
a="1,2,3,4,5";  
println(Strsplit(a,","));  
Windispg();  
```  
この出力結果は、リスト型で  
```  
[1,2,3,4,5]  
```  
となる。  
  
同様の関数に，Cindyscript の `tokenize(文字列, 文字列)` がある。`tokenize()` の第２引数は文字列や，文字のリ  
ストでもよい。  
  
#### `Fracform(数 , 分母のリストまたは最大値 [, 許容誤差の桁数 (5)])`  
分母リストの 1 つを分母とする近似分数と誤差を返す。  
戻り値は，簡易 TeX-like 書式の文字列，誤差，分子，分母。  
【例】  
```  
Ketinit();  
println(Fracform(2*pi,1..100););//2piを分母が1から100までの中で最も近い値を取る分数に近似してその簡易 TeX-like 書式の文字列、その分数の分子、分母、誤差を計算し、表示する  
println(Fracform(2*pi,300););//2piを分母が1から300までの中で最も近い値を取る分数に近似してその簡易 TeX-like 書式の文字列、その分数の分子、分母、誤差を計算し、表示する  
println(Fracform(0.1234,1000););//0.1234を分母が1から1000までの中で最も近い値を取る分数に近似してその簡易 TeX-like 書式の文字列、その分数の分子、分母、誤差を計算し、表示する  
Windispg();  
```  
  
#### `Tonormalform(簡易 TeX-like 文字列)`  
\*を省略した文字列に\*を補った文字列を返す。  
  
【例】  
```  
Ketinit();  
println(Tonormalform("ax+b"));//ax+bに乗算記号を付記して表示する  
  
Windispg();  
```  
  
この出力結果は、  
```  
a*x+b  
```  
となる。  
  
#### `Sla2fra(文字列)`  
文字列の中の / を簡易 TeX-like 書式の分数に直す。  
  
【例】  
```  
Ketinit();  
println(Sla2fra(("(4+pi)/3")));//(4+pi)/3を簡易 TeX-like 書式の分数に変換する  
  
Windispg();  
```  
  
# KeTCindy3D  
## 概要  
KeTCindy3Dの描画は次のように構成される.  
  
Cinderellaの画面に白の短径で囲んだ領域が2つできる.  
NE, SWを対角とする左側の領域を主画面, 右側の領域を副画面という.  
  
主画面は平面の場合と同様, $$\TeX$$に出力される範囲を示し,  
NE, SWの2点をドラッグすることにより変更できる.  
主画面の下方のスライダーで視点が移動でき,  
主画面上では軸が回転する.  
副画面はx, y平面上に視点を置いたものと考えればよい.  
  
主画面上にCinderellaの作図ツールで点や線分を作図すると,  
副画面に対応する点が作図される.  
主画面上の点をドラッグするとx, y座標を変更でき,  
副画面上の点をドラッグするとz座標を変更できる.  
  
KeTCindy3Dでは, 線や面についての陰線処理を行う.  
陰線処理はC言語との連携により処理を速めている  
(C言語を使う環境整備が必要であるが, 現在はこれを標準としている).  
  
## 設定  
  
#### `Ketinit3d()`  
KeTCindy3Dの使用宣言.  
Cinderellaの画面を3Dモードにする.  
  
Cinderellaの画面に視点移動のための2つのスライドを作る.  
スライダーは初期位置が左端になる.  
スライダーTHで角THETAを, スライダーFIで角PHIを内部変数として定義する.  
引数に0を入れて`Ketinit3d(0)`とすると, 副画面を表示しない.  
  
**この関数はInitializationスロットに置く.**  
**`Ketinit()`も平面の場合と異なりInitializationスロットに置く.**  
KeTCindy3Dにおける変数の初期化などを行う`Start3d()`はDrawスロットに書く.  
  
#### `Setangle(TH, FI)`  
回転角の指定.  
スライダーで設定できる回転角(視点の位置)THとFIを度数法で指定する.  
例えば`Setangle(70, 40)`とするとTH, FIがその位置になる.  
スライダーは固定されるので再度スライダーを有効にしたい場合はコメント化して再実行する.  
初期状態だけを決めたい場合は  
  
```js  
if(!Ptselected(), Setangle(70, 40));  
```  
  
または  
  
```js  
if(!Isangle(), Setangle(70, 40));  
```  
とすると, スライダーは有効となりスライダーのいずれかの点を選択した状態であればFigureボタンも有効である.  
画面上の何もないところをクリックして,  
点の選択状態を解除すると元に戻る  
  
#### `Start3d(option)`  
3Dの画面設定と空間点の認識  
副画面を作り, 幾何点を3Dの点として認識する.  
この関数は必須で, Drawスロットの先端に書く.  
  
Cinderellaの作図ツールで点・線分を作図すると, 内部関数の`Ptseg3data()`  
によってそれらを空間の点として認識し, 副画面上に対応する点をとる.  
但し, 始めはz座標をを0とする.  
点の名前がAであれば副画面上の点はAzとなる.  
点をポイントとして選択すると副画面の上に座標が表示される.  
  
作図した点の名称をインスペクタで変更した場合, 新しい名称に対応する点を副画面上に作成するが,  
以前の点は消えないので要注意.  
例えば点Aを作図した後, 主画面上の点Aをインスペクタで点Dに変えた場合,  
副画面上に新たにDzができるが, 以前のAzも残る.  
残ったAzは洗濯しておいて作図ツールの消去ボタンで消すことができる.  
`option`に除外点のリストを与えるとその点は空間点としない(スライダーで視点を移動しても位置は変わらない).  
  
## `Startsurf(options)`  
曲面描画の初期化と定数の設定.  
`options`で定数を設定する.  
定数としては分割数, Cのサイズ, 誤差の限界を設定する.  
  
`options`がないときは以下の初期設定を用いる.  
- 分割数: [50, 50]  
- Cのサイズ: [1500, 500, 200]  
- 誤差の限界: [0.01, 0.1]  
  
設定後に初期値にリセットするときは文字列`reset`を引数に与える.  
これにより陰線処理を伴う面の描画手順は, 次のようになる.  
1. `Startsurf()`で面描画の宣言をする.  
1. 描画関数でプロットデータを作る.  
1. `ExeccmdC()`でC言語を用いてまとめて描画する.  
  
#### `Xyzaz3data(name, range_x, range_y, range_z, options)`  
座標軸を描く.  
画面に座標軸を描き, プロットデータ`ax3d`を作成する.  
`name`は空文字列でよい.  
  
`options`は次の2つ.  
- 矢じり`an`: nは数字で矢じりの大きさ(nはなくてもよい).  
- 原点$$\mathrm{O}$$`Onesw`: `nesw`は微小位置. 数字も付けられる(`nesw`をつけない場合, 初期値は`sw`).  
  
【例】初期設定の座標軸.  
```js  
Xyzax3data("", "x=[-5, 5]", "y=[-5, 5]", "z=[-5, 5]");  
```  
矢じりをつける.  
```js  
Xyzax3data("", "x=[-5, 5]", "y=[-5, 5]", "z=[-5, 5]", "a");  
```  
矢じりを大きくする.  
```js  
Xyzax3data("", "x=[-5, 5]", "y=[-5, 5]", "z=[-5, 5]", ["a2"]);  
```  
原点$$\mathrm{O}$$を表示する.  
```js  
Xyzax3data("", "x=[-5, 5]", "y=[-5, 5]", "z=[-5, 5]", ["O"]);  
```  
原点$$\mathrm{O}$$の位置を調整して右上に表示する. 矢じりもつける.  
```js  
Xyzax3data("", "x=[-5, 5]", "y=[-5, 5]", "z=[-5, 5]", ["a", "Oe2n2"]);  
```  
  
